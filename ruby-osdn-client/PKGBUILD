# Maintainer: artoo <artoo@manjaro.org>

_gemname=osdn-client
pkgname=ruby-${_gemname}
pkgver=0.0.20160304
pkgrel=1
pkgdesc='OSDN API basic client library for ruby'
arch=('any')
url='https://osdn.jp/'
license=('MIT')
depends=('ruby-typhoeus' 'ruby-json')
options=('!emptydirs')
noextract=("${_gemname}-${pkgver}.gem")
source=("https://rubygems.org/downloads/${_gemname}-${pkgver}.gem"
		'LICENSE')
sha256sums=('1044f42e8174b60399694be170eb558ea67000a274c87ba97ecf8ef696c6bb65'
            'f197efde12656d9e2be81ff8030c1e80fcbc09f399d99c0b16110fcdaa8d37d3')

package() {
		local _gemdir="$(ruby -e'puts Gem.default_dir')"
		gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

		rm -rf "${pkgdir}/${_gemdir}/cache"

		rm -rf "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/ext"

		install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
