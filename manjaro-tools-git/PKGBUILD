# Maintainer: artoo <artoo@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

_pkgbase=manjaro-tools
_ver=0.13.7
_branch=master

pkgbase=manjaro-tools-git
pkgname=('manjaro-tools-base-git'
        'manjaro-tools-pkg-git'
        'manjaro-tools-iso-git'
        'manjaro-tools-yaml-git')
pkgver=r2292.3faed98
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Development tools for Manjaro Linux'
license=('GPL')
groups=('manjaro-tools')
url='https://github.com/manjaro/manjaro-tools'
depends=('python-yaml') #for use check-yaml
makedepends=('git' 'docbook2x')
source=("git+$url.git#branch=$_branch")
sha256sums=('SKIP')

prepare() {
    cd ${srcdir}/${_pkgbase}
    # patches here
    sed -e "s/^Version=.*/Version=$_ver/" -i Makefile
    #sed -e "s/^Version=.*/Version=$pkgver/" -i Makefile
}

pkgver() {
    cd ${srcdir}/${_pkgbase}
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr
}

package_manjaro-tools-base-git() {
    pkgdesc='Development tools for Manjaro Linux (base tools)'
    provides=("manjaro-tools-base=$_ver")
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman-mirrorlist>=20160301')
    optdepends=('manjaro-tools-pkg: Manjaro Linux package tools'
                    'manjaro-tools-iso: Manjaro Linux iso tools')
    conflicts=('manjaro-tools-base')
    backup=('etc/manjaro-tools/manjaro-tools.conf')

    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_manjaro-tools-pkg-git() {
    pkgdesc='Development tools for Manjaro Linux (packaging tools)'
    provides=("manjaro-tools-pkg=$_ver")
    depends=('namcap' 'manjaro-tools-base-git')
    conflicts=('manjaro-tools-pkg' 'devtools')

    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_manjaro-tools-iso-git() {
    pkgdesc='Development tools for Manjaro Linux (ISO tools)'
	provides=("manjaro-tools-iso=$_ver")
    depends_i686=('nbd' 'libisoburn' 'squashfs-tools' 'syslinux' 'isolinux'
                'manjaro-tools-base-git' 'mkinitcpio-nfs-utils'
                'manjaro-iso-profiles-base' 'mktorrent')
    depends_x86_64=( ${depends_i686[@]} 'manjaro-efi-shell' )
    optdepends=('manjaro-iso-profiles-official: Manjaro Linux ISO-Image official profiles'
                'manjaro-iso-profiles-community: Manjaro Linux ISO-Image community profiles'
                'manjaro-iso-profiles-minimal: Manjaro Linux ISO-Image minimal profiles'
                'manjaro-tools-yaml: Majaro Linux yaml validation tools'
                'qemu: quickly test isos')
	conflicts=('manjaro-tools-iso')

    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

package_manjaro-tools-yaml-git() {
    pkgdesc='Development tools for Manjaro Linux (yaml tools)'
    provides=("manjaro-tools-yaml=$_ver")
    depends=('manjaro-tools-iso-git' 'python-pykwalify')
    conflicts=('manjaro-tools-yaml')

    cd ${srcdir}/${_pkgbase}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_yaml
}
