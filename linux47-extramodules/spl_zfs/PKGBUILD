# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

_linuxprefix=linux47
_extramodules=extramodules-4.7-MANJARO
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgbase=$_linuxprefix-spl_zfs
pkgname=("$_linuxprefix-spl" "$_linuxprefix-zfs")
pkgver=0.6.5.7
pkgrel=0.4
url="http://zfsonlinux.org/"
arch=('i686' 'x86_64')
license=("CDDL")
depends=("$_linuxprefix" "kmod")
makedepends=("$_linuxprefix-headers")
groups=("$_linuxprefix-extramodules")
install=install
source=("http://archive.zfsonlinux.org/downloads/zfsonlinux/spl/spl-${pkgver}.tar.gz"
        "http://archive.zfsonlinux.org/downloads/zfsonlinux/zfs/zfs-${pkgver}.tar.gz"
         linux47-spl-01.patch
         linux47-spl-02.patch
         linux47-zfs-01.patch::https://github.com/zfsonlinux/zfs/commit/fd4c7b7a73fda391f94f58530c86ffa5b2ef8e6f.patch
         linux47-zfs-02.patch::https://github.com/zfsonlinux/zfs/commit/68e8f59afb0fa1b388c7dbb8720ac6756d390146.patch
         linux47-zfs-03.patch::https://github.com/zfsonlinux/zfs/commit/9baaa7deae45c8556dfd79b2011234da5cb37b3a.patch
         linux47-zfs-04.patch::https://github.com/zfsonlinux/zfs/commit/8fbbc6b4cf13f73d517ec4e826a7069a958fa5ba.patch)
sha256sums=('dc8690e407183eeb7a6af0e7692d6e0a1cd323d51dd1aa492522c421b1924ea0'
            '4a9e271bb9a6af8d564e4d5800e4fff36224f1697b923a7253659bdda80dc590'
            '10f50adcb94c1964e03e5c540c4cb19e3161bb1021a55cdb245da409b2ef2a44'
            'a53d2a06c038dddc980664fd4b4ba33b1c25e55682893424f63c53c0e49b51e0'
            '5a92f719556a4f2566bd1130aa5cdefba2abb8cfddcfd094f04ee05480ff31cc'
            '99ba07b3a49fa0ca299cc53b2a82a9da14f7a90c15d7f1bfdef265f35fcc84da'
            '49e7d35921b71ee5b15daef312ad1813c181cbb52bb3087286d7dc923b4dc068'
            'c42f68f3e695ddbf14f34cbcdf1bd9243a0a1b75860c2f5a04b9ba02afb81aee')

prepare() {
  cd "${srcdir}/spl-${pkgver}"
  patch -Np1 -i $srcdir/linux47-spl-01.patch
  patch -Np1 -i $srcdir/linux47-spl-02.patch
  cd "${srcdir}/zfs-${pkgver}"
  patch -Np1 -i $srcdir/linux47-zfs-01.patch
  patch -Np1 -i $srcdir/linux47-zfs-02.patch
  patch -Np1 -i $srcdir/linux47-zfs-03.patch
  patch -Np1 -i $srcdir/linux47-zfs-04.patch
}

build() {
  cd "${srcdir}/spl-${pkgver}"
  ./autogen.sh
  sed -i "s|\$(uname -r)|${_kernver}|g" configure
  ./configure --prefix=/usr --libdir=/usr/lib --sbindir=/usr/bin \
              --with-linux=/usr/lib/modules/${_kernver}/build \
              --with-config=kernel
  make
  cd "${srcdir}/zfs-${pkgver}"
  ./autogen.sh
  sed -i "s|\$(uname -r)|${_kernver}|g" configure
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --libdir=/usr/lib \
              --datadir=/usr/share --includedir=/usr/include --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-${pkgver} --with-config=kernel \
              --with-linux=/usr/lib/modules/${_kernver}/build \
              --with-spl-obj=${srcdir}/spl-${pkgver}
  make
}

package_linux47-spl(){
  _pkgname=spl
  pkgdesc='Solaris Porting Layer kernel modules.'
  provides=("$_pkgname=$pkgver")
  depends+=("spl-utils=${pkgver}")

  cd "${srcdir}/spl-${pkgver}"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
  install -m644 module/*/*.ko "$pkgdir/usr/lib/modules/$_extramodules"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/install"
}

package_linux47-zfs(){
  _pkgname=zfs
  pkgdesc='Kernel modules for the Zettabyte File System.'
  provides=("$_pkgname=$pkgver")
  depends+=("$_linuxprefix-spl" "zfs-utils=${pkgver}")

  cd "${srcdir}/zfs-${pkgver}"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
  install -m644 module/*/*.ko "$pkgdir/usr/lib/modules/$_extramodules"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/install"
}
