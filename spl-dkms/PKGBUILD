# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=("spl-dkms")
pkgver=0.6.5.2
pkgrel=2
pkgdesc="Solaris Porting Layer kernel modules."
depends=("spl-utils=${pkgver}" "dkms")
arch=("any")
url="http://zfsonlinux.org/"
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/spl/spl-${pkgver}.tar.gz
        4.5-support-pfn_t-typedef.patch::https://github.com/zfsonlinux/spl/commit/de77e24.patch)
groups=("manjarozfs")
sha256sums=('ada7892a77e3a179bef633be7afad6e04f472b7e7579591fdd86957149408fc7'
            'dbff388167711fd60c2d04e46e44c7d727a6d178e81dbf86b70086f8fdf2ab78')
license=("GPL")
install=spl-dkms.install

prepare() {
  cd "$srcdir/spl-${pkgver}"
  patch -Np1 -i $srcdir/4.5-support-pfn_t-typedef.patch
}

build() {
  cd "$srcdir/spl-${pkgver}"
  ./autogen.sh
  scripts/dkms.mkconf -v ${pkgver} -f dkms.conf -n spl
}

package() {
   install -d ${pkgdir}/usr/src/spl-${pkgver}
   cp -a ${srcdir}/spl-${pkgver}/ ${pkgdir}/usr/src/
}
