# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>

pkgname=manjaro-settings-manager
pkgver=0.3.2
pkgrel=1
pkgdesc="Manjaro Linux System Settings Tool"
arch=('i686' 'x86_64')
url="http://git.manjaro.org"
license=('GPL')
depends=('keyboardctl' 'locale-info' 'qt5-webkit' 'icu' 'hwinfo' 'kitemmodels')
makedepends=('qt5-tools')
optdepends=('kdesu: for KDE'
            'gksu: for XFCE, Gnome, LXDE, Cinnamon'
            'gnome-keyring: for password management'
            'polkit: as an alternative su tool')
replaces=('manjaro-system-settings')
provides=('manjaro-system-settings')
conflicts=('manjaro-system-settings')
install=install
source=("https://github.com/manjaro/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('e219b3394f96c82b7f164d693e29b8760d3e1425c5f986994693758699c0c526')


build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  /usr/lib/qt/bin/lrelease $pkgname/$pkgname.pro
  /usr/lib/qt/bin/lrelease $pkgname-daemon/$pkgname-daemon.pro

  qmake-qt5

  # Build fix
  make sub-global-all
  cp global/libglobal.a libglobal.a

  make all
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"
  make INSTALL_ROOT="${pkgdir}" install
}
