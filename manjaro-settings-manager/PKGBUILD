# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>

pkgname=manjaro-settings-manager
pkgver=0.4.13
pkgrel=1
pkgdesc="Manjaro Linux System Settings Tool"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/manjaro-settings-manager"
license=('GPL')
depends=('ckbcomp' 'qt5-base' 'icu' 'hwinfo' 'kitemmodels' 'libxkbcommon-x11')
makedepends=('qt5-tools')
optdepends=('kdesu4: for KDE 4'
            'kde-cli-tools: for Plasma 5'
            'gksu: for XFCE, Gnome, LXDE, Cinnamon'
            'gnome-keyring: for password management'
            'polkit: as an alternative su tool')
replaces=('manjaro-system-settings')
provides=('manjaro-system-settings')
conflicts=('manjaro-system-settings')
install=$pkgname.install
source=("https://github.com/manjaro/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('7eaedbab517ba9311f09dbb5455e95ab0c8ad810559251d92d629f7568e352bb')

prepare() {
  cd "$srcdir/$pkgname-$pkgver/"
  # patches here
}

build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  /usr/lib/qt/bin/lrelease $pkgname/$pkgname.pro
  /usr/lib/qt/bin/lrelease $pkgname-daemon/$pkgname-daemon.pro

  qmake-qt5

  # Build fix
  make sub-global-all
  cp global/libglobal.a libglobal.a

  make all
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"
  make INSTALL_ROOT="${pkgdir}" install
}
