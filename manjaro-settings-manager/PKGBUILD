# Maintainer : Ramon Buldo <ramon@manjaro.org>

pkgbase=manjaro-settings-manager
pkgname=('manjaro-settings-manager' 'manjaro-settings-manager-kcm')
pkgver=0.4.95
pkgrel=2
_pkgver=0.4.95
pkgdesc="Manjaro Linux System Settings Tool"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/manjaro-settings-manager"
license=("GPL")
depends=('icu' 'qt5-base' 'hwinfo' 'kitemmodels' 'kauth' 
         'kcoreaddons' 'ckbcomp' 'knotifications' 'xdg-utils')
makedepends=('git' 'extra-cmake-modules' 'kdoctools' 'qt5-tools'
             'kcmutils' 'kconfigwidgets')
conflicts=('kcm-msm')
install=${pkgname}.install
source=(git+$url.git)
sha256sums=(SKIP)

#pkgver() {
#  cd "$srcdir/manjaro-settings-manager"
#  _git=".r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
#  echo -e "${_pkgver}${_git}"
#}

build() {
  cd "$srcdir/manjaro-settings-manager"
  mkdir -p build
  cd build
  cmake ../ \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package_manjaro-settings-manager() {
  cd "$srcdir/manjaro-settings-manager/build"
  make DESTDIR=${pkgdir} install  
  rm -rf $pkgdir/usr/lib/qt
  rm -rf $pkgdir/usr/share/kservices5
}

package_manjaro-settings-manager-kcm() {
  pkgdesc="Manjaro Linux System Settings Tool (KCM for Plasma 5)"
  depends=('manjaro-settings-manager' 'kcmutils' 'kconfigwidgets')
  replaces=('kcm-msm')
  cd "$srcdir/manjaro-settings-manager/build"
  make DESTDIR=${pkgdir} install
  rm -rf $pkgdir/etc  
  rm -rf $pkgdir/usr/bin
  rm -rf $pkgdir/usr/lib/kauth
  rm -rf $pkgdir/usr/share/{applications,dbus-1,icons,polkit-1}  
}
