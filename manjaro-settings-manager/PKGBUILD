# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>

pkgname=manjaro-settings-manager
pkgver=0.4.12
pkgrel=2
pkgdesc="Manjaro Linux System Settings Tool"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/manjaro-settings-manager"
license=('GPL')
depends=('ckbcomp' 'qt5-base' 'icu' 'hwinfo' 'kitemmodels' 'libxkbcommon-x11')
makedepends=('qt5-tools')
optdepends=('kdesu4: for KDE 4'
            'kde-cli-tools: for Plasma 5'
            'gksu: for XFCE, Gnome, LXDE, Cinnamon'
            'gnome-keyring: for password management'
            'polkit: as an alternative su tool')
replaces=('manjaro-system-settings')
provides=('manjaro-system-settings')
conflicts=('manjaro-system-settings')
install=$pkgname.install
source=("https://github.com/manjaro/$pkgname/archive/$pkgver.tar.gz"
        "notification.patch::https://github.com/manjaro/manjaro-settings-manager/commit/9b1a3d59072bee327ca1e046a5df3763a84e1538.patch")
sha256sums=('50f45758a3f439c03d272b30a110883dc41f9a9119305c02321358fea6279905'
            '51de9a5c9b5464a27db44e07f546f81834631101a7193bb27512075be1e4f85c')

prepare() {
  cd "$srcdir/$pkgname-$pkgver/"
  # patches here
  patch -Np1 -i "$srcdir/notification.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  /usr/lib/qt/bin/lrelease $pkgname/$pkgname.pro
  /usr/lib/qt/bin/lrelease $pkgname-daemon/$pkgname-daemon.pro

  qmake-qt5

  # Build fix
  make sub-global-all
  cp global/libglobal.a libglobal.a

  make all
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"
  make INSTALL_ROOT="${pkgdir}" install
}
