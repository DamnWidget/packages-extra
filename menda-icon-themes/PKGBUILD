# Maintainer: Alexandru Ianu <(alexandru . ianu @ gmail . com> / Bernhard Landauer <oberon@manjaro.org>
# Contributor: Rob McCathie <rob@manjaro.org>
# Contributor: Bernhard Landauer <oberon@manjaro.org>

pkgbase=menda-icon-themes
pkgname=('menda-circle-icon-theme' 'menda-maia-icon-theme')
pkgver=20160416
pkgrel=1
_git='19d251bce23ba64f30333dcddffb719bc813c504'
arch=('any')
url="https://github.com/manjaro/menda-icon-themes"
license=('GPL')
depends=('hicolor-icon-theme' 'librsvg')
options=('!emptydirs' '!strip')
source=("${pkgbase}-${pkgver}-${pkgrel}.tar.gz::${url}/archive/${_git}.tar.gz")
md5sums=('e5ddd179c6b46af66b305c54fb4c3dc0')

package_menda-circle-icon-theme() {
  pkgdesc="Menda Circle Icon Theme"
  
  install -dm755 "${pkgdir}/usr/share/icons"
  #install -Dm755 "${srcdir}/${pkgbase}-${_git}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd "${pkgdir}/usr/share/icons"
  cp -r "${srcdir}/${pkgbase}-${_git}/Menda-Circle" .
  find . -type d -exec chmod 755 '{}' \;
  find . -type f -exec chmod 644 '{}' \;
}

package_menda-maia-icon-theme() {
  pkgdesc="Menda Circle Icons with Maia colors"
  optdepends=('vertex-maia-themes: Official Manjaro Gtk2, Gtk3, Metacity, Xfwm, Openbox, Cinnamon and GNOME Shell themes')

  install -dm755 "${pkgdir}/usr/share/icons"
  #install -Dm755 "${srcdir}/${pkgbase}-${_git}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd "${pkgdir}/usr/share/icons"
  cp -r "${srcdir}/${pkgbase}-${_git}/Menda-Circle" Menda-Maia
  find . -type d -exec chmod 755 '{}' \;
  find . -type f -exec chmod 644 '{}' \;

  msg "Adjusting icon colors..." 
  find . -name "*.svg" -exec sed -i "s/27ae60/16a085/g" '{}' \;
  find . -name "index.*" -exec sed -i "s/Circle/Maia/g" '{}' \;
}
