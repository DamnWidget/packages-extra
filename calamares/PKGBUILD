# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgname=calamares
pkgver=2.3
pkgrel=2
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares"
license=('LGPL')
depends=('kconfig' 'kcoreaddons' 'ki18n' 'solid' 'yaml-cpp' 'kpmcore>=2.2.0' 'mkinitcpio-openswap'
         'boost-libs' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gptfdisk' 'gtk-update-icon-cache')
makedepends=('extra-cmake-modules' 'qt5-tools' 'git' 'boost')
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source+=("$pkgname-$pkgver.tar.gz::https://github.com/calamares/calamares/releases/download/v$pkgver/calamares-$pkgver.tar.gz"
         git+https://github.com/calamares/calamares-manjaro.git#branch=2.3.x-stable
         prefer_budgie_before_gnome.patch::https://github.com/calamares/calamares/commit/8cbefa12e042ac85239b0312d4477cb21c241823.patch
         machine-id.patch::https://github.com/calamares/calamares/commit/23789675c819803fa966a9f39cb21e64a0785846.patch
         proper-machine-id.patch::https://github.com/calamares/calamares/commit/ca9f4bc05348f11dc30eaa825839433d9eb24d29.patch
         simplify-machine-id.patch::https://github.com/calamares/calamares/commit/8768c3af6664053e9f4e7292bd6bf25f67fdef52.patch)

sha256sums=('c499a8a8ea1573c6f0323398fb092f8978958202e11c9d339202c35c73276f9f'
            'SKIP'
            '91ae6e3f0d249d57ad37361b8606883da5652eeebdb5819f9bc40b1d4f28746f'
            '013db34dee4117ce2c1239523583edfeaafc80258d6808cc1d128d4cc19d044e'
            'ac042dd3d8983dc56fb3b511c69ec691bc3c8590a2366577c01b7a46c604d2ee'
            '676a4a6c23654fef5a4ed88c80b102a8cb57cf3c5771229bcf2f9def077394ee')

prepare() {
	cp -a ${srcdir}/calamares-manjaro/* ${srcdir}/calamares-${pkgver}
	rm -r ${srcdir}/calamares-manjaro
	cd ${srcdir}/calamares-${pkgver}
	patch -Np1 -i ${srcdir}/prefer_budgie_before_gnome.patch
	patch -Np1 -i ${srcdir}/machine-id.patch
	patch -Np1 -i ${srcdir}/proper-machine-id.patch
	patch -Np1 -i ${srcdir}/simplify-machine-id.patch
}

build() {
	cd ${srcdir}/calamares-${pkgver}

	mkdir -p build
	cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DSKIP_MODULES="webview interactiveterminal"
        make
}

package() {
	cd ${srcdir}/calamares-${pkgver}/build
	make DESTDIR="$pkgdir" install
	install -Dm644 "../conf/bootloader.conf" "$pkgdir/usr/share/calamares/modules/bootloader.conf"
	install -Dm644 "../conf/initcpio.conf" "$pkgdir/usr/share/calamares/modules/initcpio.conf"
	install -Dm644 "../conf/services.conf" "$pkgdir/usr/share/calamares/modules/services.conf"
	install -Dm644 "../conf/unpackfs.conf" "$pkgdir/usr/share/calamares/modules/unpackfs.conf"
	install -Dm644 "../conf/users.conf" "$pkgdir/usr/share/calamares/modules/users.conf"
	install -Dm644 "../conf/settings.conf" "$pkgdir/usr/share/calamares/settings.conf"
	install -Dm644 "../data/manjaro-icon.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/calamares.svg"
	install -Dm644 "../data/calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
	install -Dm755 "../data/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	install -Dm644 "../data/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
	chmod 750      "$pkgdir"/etc/polkit-1/rules.d
	rm -r "$pkgdir/usr/lib/calamares/modules/dummyprocess/"
	rm -r "$pkgdir/usr/lib/calamares/modules/dummypython/"
	rm "$pkgdir/usr/share/calamares/modules/dummypython.conf"
}
