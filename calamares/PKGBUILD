# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgname=calamares
pkgver=2.2.3rc1
pkgrel=2
_pkgver=2.2.2
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares"
license=('LGPL')
depends=('kconfig' 'kcoreaddons' 'ki18n' 'solid' 'yaml-cpp' 'kpmcore>=2.2.0'
         'boost-libs' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gptfdisk' 'gtk-update-icon-cache')
makedepends=('extra-cmake-modules' 'qt5-tools' 'git' 'boost')
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source+=("$pkgname-$_pkgver.tar.gz::https://github.com/calamares/calamares/releases/download/v$_pkgver/calamares-$_pkgver.tar.gz"
         git+https://github.com/calamares/calamares-manjaro.git#branch=2.2.x-stable
         prefer_budgie_before_gnome.patch::https://github.com/calamares/calamares/commit/8cbefa12e042ac85239b0312d4477cb21c241823.patch
         "v2.2.3-rollup.patch::https://github.com/calamares/calamares/compare/v$_pkgver...2.2.x-stable.patch")

sha256sums=('d9cf132ae263a48455596d4a7e522d5199c7717dc9b7f3568cefe588082667a9'
            'SKIP'
            '91ae6e3f0d249d57ad37361b8606883da5652eeebdb5819f9bc40b1d4f28746f'
            'dca82f3d7b5ed631c113acc916ac2ae833a793098816a0f26eb3b620ee0d3c72')

prepare() {
	cp -a ${srcdir}/calamares-manjaro/* ${srcdir}/calamares-${_pkgver}
	rm -r ${srcdir}/calamares-manjaro
	cd ${srcdir}/calamares-${_pkgver}
	sed -i 's|CALAMARES_VERSION_PATCH 2|CALAMARES_VERSION_PATCH 3|' CMakeLists.txt
	sed -i 's|CALAMARES_VERSION_RC 0|CALAMARES_VERSION_RC 1|' CMakeLists.txt
	patch -Np1 -i ${srcdir}/prefer_budgie_before_gnome.patch
	patch -Np1 -i ${srcdir}/v2.2.3-rollup.patch
}

build() {
	cd ${srcdir}/calamares-${_pkgver}

	mkdir -p build
	cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DSKIP_MODULES="webview interactiveterminal"
        make
}

package() {
	cd ${srcdir}/calamares-${_pkgver}/build
	make DESTDIR="$pkgdir" install
	install -Dm644 "../conf/bootloader.conf" "$pkgdir/usr/share/calamares/modules/bootloader.conf"
	install -Dm644 "../conf/initcpio.conf" "$pkgdir/usr/share/calamares/modules/initcpio.conf"
	install -Dm644 "../conf/services.conf" "$pkgdir/usr/share/calamares/modules/services.conf"
	install -Dm644 "../conf/unpackfs.conf" "$pkgdir/usr/share/calamares/modules/unpackfs.conf"
	install -Dm644 "../conf/users.conf" "$pkgdir/usr/share/calamares/modules/users.conf"
	install -Dm644 "../conf/settings.conf" "$pkgdir/usr/share/calamares/settings.conf"
	install -Dm644 "../data/manjaro-icon.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/calamares.svg"
	install -Dm644 "../data/calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
	install -Dm755 "../data/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	install -Dm644 "../data/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
	chmod 750      "$pkgdir"/etc/polkit-1/rules.d
	rm -r "$pkgdir/usr/lib/calamares/modules/dummyprocess/"
	rm -r "$pkgdir/usr/lib/calamares/modules/dummypython/"
	rm "$pkgdir/usr/share/calamares/modules/dummypython.conf"
}
