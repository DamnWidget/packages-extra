# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgname=calamares
pkgver=3.0.1
_pkgver=3.0.1
pkgrel=5
_commit=v3.0.1
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares"
license=('LGPL')
depends=('kconfig' 'kcoreaddons' 'ki18n' 'solid' 'yaml-cpp' 'kpmcore>=3.0.2' 'mkinitcpio-openswap'
         'boost-libs' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gtk-update-icon-cache' 'pythonqt')
makedepends=('extra-cmake-modules' 'qt5-tools' 'git' 'boost')
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source+=(#"$pkgname-$pkgver.tar.gz::https://github.com/manjaro/calamares/archive/v${_pkgver}.tar.gz"
         "$pkgname-$pkgver-$pkgrel.tar.gz::https://github.com/manjaro/calamares/archive/${_commit}.tar.gz"
         "has-internet.patch::https://github.com/manjaro/calamares/commit/8215a633e27e8fd9061a91dc89d7bfdeea784228.patch"
         "dev-path.patch::https://github.com/manjaro/calamares/commit/06b1d4abd2412b79242a105ca289198a0d36b3bf.patch"
         "pkg-robust.patch::https://github.com/manjaro/calamares/commit/8804b15b59138850bede90ce84d3212dbb391be2.patch"
         "chrootcfg-patch::https://github.com/manjaro/calamares/commit/e6c4efbb110372322e0033886d5aed77f01b6c71.patch"
         "double-click.patch::https://github.com/manjaro/calamares/commit/d1f3c2d73bb7fbd18b24dc44f1928bcea9cb1fc7.patch")
sha256sums=('2ce8aaea30e352c75567ad93e99144dad444540e4a3ca91e731c0f297488181a'
            'd1d4622087c4bc2f822701aa61dd10b2b6b2eb8b28c2ef166e283f10576a8dc1'
            '0b16b30464fae627146a13abbeaf3a62a3e4f2c5037f24157d189c2125220ef9'
            '68244ebe7173012a80b9c76f287fc9a42ab3dc4311c5addafd4450c4f5f41c60'
            '55c46aebac27fdb12d85f11405761cc6ece69cc3af9079c81d6b4c14844c4967'
            '40e328a0d7bb904fdbbd8891a476c855f8490f781edaa42a753c29059f25a601')

prepare() {
	#mv ${srcdir}/calamares-${_commit} ${srcdir}/calamares-${_pkgver}
	cd ${srcdir}/calamares-${_pkgver}
	# patches here
	patch -p1 -i $srcdir/has-internet.patch
	patch -p1 -i $srcdir/dev-path.patch
	patch -p1 -i $srcdir/pkg-robust.patch
	patch -p1 -i $srcdir/chrootcfg-patch
	patch -p1 -i $srcdir/double-click.patch
}

build() {
	cd ${srcdir}/calamares-${_pkgver}

	mkdir -p build
	cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DWITH_PYTHONQT:BOOL=ON \
              -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg dracut dracutlukscfg \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt"
        make
}

package() {
	cd ${srcdir}/calamares-${_pkgver}/build
	make DESTDIR="$pkgdir" install
	install -Dm644 "../data/manjaro-icon.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/calamares.svg"
	install -Dm644 "../data/calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
	install -Dm755 "../data/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	install -Dm644 "../data/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
	chmod 750      "$pkgdir"/etc/polkit-1/rules.d
}
