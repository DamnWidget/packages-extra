# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-addons
pkgver=5.6.0
pkgrel=1.1
pkgdesc='All kind of addons to improve your Plasma experience'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kdeplasma-addons'
license=('LGPL')
depends=('plasma-workspace')
makedepends=('extra-cmake-modules' 'kdoctools' 'kross' 'purpose')
optdepends=('kross: comic applet'
            'purpose: Quickshare applet'
            'quota-tools: disk quota applet')
install="${pkgname}.install"
conflicts=('kdebase-workspace' 'kdeplasma-addons-frameworks')
replaces=('kdeplasma-addons-frameworks')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"
        "showdesktop_fix_sizing_of_the_plasmoid_in_the_panel.patch::http://quickgit.kde.org/?p=kdeplasma-addons.git&a=commitdiff&h=e164ae7c02f5579e02d26842bee6a4a450e01871&o=plain")
md5sums=('9e1892505d9277a650f5de2a991dfb10'
         '2d82b842f8ce5b1d26dc37bb448bcd54')

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  patch -Np1 -i ../showdesktop_fix_sizing_of_the_plasmoid_in_the_panel.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
