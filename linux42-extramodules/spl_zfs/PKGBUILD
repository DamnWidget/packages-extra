# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgbase=linux42-spl_zfs
pkgname=('linux42-spl' 'linux42-zfs')
pkgver=0.6.4.2
pkgrel=0.1
url="http://zfsonlinux.org/"
arch=('i686' 'x86_64')
license=("CDDL")
depends=('linux42')
makedepends=("spl-dkms>=$pkgver" "zfs-dkms>=$pkgver"
             'dkms'
             'linux42' 'linux42-headers')
groups=('linux42-extramodules')
install=install

source=("0001-bdi_setup_and_register.patch::https://github.com/zfsonlinux/zfs/commit/e80da86447174b583e4b6cebedae2956fac926ee.patch"
        "0002-remove-bio-bi_cnt-access.patch::https://github.com/zfsonlinux/zfs/commit/7eb333fbdde32dbebdcc88c35610159e207237c9.patch"
        "0003-follow_link-to-put_link.patch::https://github.com/zfsonlinux/zfs/commit/bd29109f1ac5be68f7f7c8bcb49e1b706fe899f0.patch")

md5sums=('5adc3ac3f9f9f52316448ae3f2372904'
         '6965ddae0990ab7198bec38de996c120'
         '394af99f8d22e4da2c0e3d43a4e8b159')

prepare() {
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf

  # workaround to patch for linux 4.2
  rm -r $srcdir/dkms/zfs/$pkgver/source
  cp -r /usr/src/zfs-$pkgver $srcdir/dkms/zfs/$pkgver/source
  cd dkms/zfs/$pkgver/source
  patch -Np1 -i $srcdir/0001-bdi_setup_and_register.patch
  patch -Np1 -i $srcdir/0002-remove-bio-bi_cnt-access.patch
  patch -Np1 -i $srcdir/0003-follow_link-to-put_link.patch
}

build() {
  _extraver=extramodules-4.2-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"

  # build host modules
  msg2 'Build modules'
  dkms --dkmsframework dkms.conf build "spl/$pkgver" -k "$_kernver"
  dkms --dkmsframework dkms.conf build "zfs/$pkgver" -k "$_kernver"
}

package_linux42-zfs(){
  _extraver=extramodules-4.2-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"
  _pkgname=zfs
  pkgdesc='Kernel modules for the Zettabyte File System.'
  provides=("$_pkgname=$pkgver")
  depends=('linux42' "zfs-utils=${pkgver}")

  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/zfs/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/install"
}

package_linux42-spl(){
  _extraver=extramodules-4.2-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"
  _pkgname=spl
  pkgdesc='Solaris Porting Layer kernel modules.'
  provides=("$_pkgname=$pkgver")
  depends=('linux42' "spl-utils=${pkgver}")

  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/spl/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/install"
}
