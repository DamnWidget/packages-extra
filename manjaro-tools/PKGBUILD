# Maintainer: artoo <artoo@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

pkgbase=manjaro-tools
pkgname=('manjaro-tools-base' 'manjaro-tools-pkg' 'manjaro-tools-iso')
pkgver=0.12.1
pkgrel=3
pkgdesc='Development tools for Manjaro Linux'
arch=('i686' 'x86_64')
license=('GPL')
groups=('manjaro-tools')
url='https://github.com/manjaro/manjaro-tools'
makedepends=('docbook2x')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
	"log_dir.patch::https://github.com/manjaro/manjaro-tools/commit/417e0ca1605699a242127aea5a2e2d064d14c73b.patch"
	"error_handling.patch::https://github.com/manjaro/manjaro-tools/commit/52ce7f86befcdce48dfea0e42539ac954971d840.patch")
sha1sums=('7359cf7b2a7e638fb91f43df41642bfe6dc3e3c4'
          '9b6b75cd7e563ac5b625d5ae3c5057c15f65e14e'
          '7c1c663787283d8e11ca0d3b2e71285c5ed50075')

prepare() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	# patches here
	patch -Np1 -i "${srcdir}/log_dir.patch"
	patch -Rp1 -i "${srcdir}/error_handling.patch"
}

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr
}

package_manjaro-tools-base() {
 	pkgdesc='Development tools for Manjaro Linux (base tools)'
	depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman-mirrorlist>=20160301')
	optdepends=('manjaro-tools-pkg: Manjaro Linux package tools'
                    'manjaro-tools-iso: Manjaro Linux iso tools')
	backup=('etc/manjaro-tools/manjaro-tools.conf')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_manjaro-tools-pkg() {
 	pkgdesc='Development tools for Manjaro Linux (packaging tools)'
	depends=('namcap' 'manjaro-tools-base')
	conflicts=('devtools')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_manjaro-tools-iso() {
 	pkgdesc='Development tools for Manjaro Linux (ISO tools)'
	depends_i686=('nbd' 'libisoburn' 'squashfs-tools' 'syslinux' 'isolinux'
                      'manjaro-tools-base' 'mkinitcpio-nfs-utils'
                      'mktorrent' 'manjaro-iso-profiles-base')
	depends_x86_64=( ${depends_i686[@]} 'manjaro-efi-shell')
	optdepends=('manjaro-iso-profiles: Manjaro Linux ISO-Image profiles'
                    'manjaro-iso-profiles-community: Manjaro Linux ISO-Image community profiles'
                    'manjaro-iso-profiles-minimal: Manjaro Linux ISO-Image minimal profiles'
                    'sonar-iso-profiles: Sonar Linux ISO-Image profiles'
                    'qemu: quickly test isos')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}
