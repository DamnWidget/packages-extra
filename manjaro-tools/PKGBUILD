# Maintainer: artoo <artoo@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

pkgbase=manjaro-tools
pkgname=('manjaro-tools-base' 'manjaro-tools-pkg' 'manjaro-tools-iso')
pkgver=0.11.6
pkgrel=2
pkgdesc='Development tools for Manjaro Linux'
arch=('i686' 'x86_64')
license=('GPL')
groups=('manjaro-tools')
url='https://github.com/manjaro/manjaro-tools'
makedepends=('docbook2x')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        'preloader.patch::https://github.com/manjaro/manjaro-tools/commit/32c9f536ccd8b2cf5eda09f8858449ad766eb60c.patch')
sha256sums=('2daa924908e58e38296603c5a49e7c6a00ccb3506e9440ddde7fdd69d5885f03'
            'a74bfb781373e4a95c12b9e4cb07ddb5327b552041a89e034b5d93b8eed54799')

prepare() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	# patches here
	patch -Np1 -i "${srcdir}/preloader.patch"
}

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr
}

package_manjaro-tools-base() {
 	pkgdesc='Development tools for Manjaro Linux (base tools)'
	depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman-mirrorlist>=20160301')
	optdepends=('manjaro-tools-pkg: Manjaro Linux package tools'
                    'manjaro-tools-iso: Manjaro Linux iso tools')
	backup=('etc/manjaro-tools/manjaro-tools.conf')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_manjaro-tools-pkg() {
 	pkgdesc='Development tools for Manjaro Linux (packaging tools)'
	depends=('namcap' 'manjaro-tools-base')
	conflicts=('devtools')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_manjaro-tools-iso() {
 	pkgdesc='Development tools for Manjaro Linux (ISO tools)'
	depends_i686=('nbd' 'libisoburn' 'squashfs-tools' 'syslinux' 'isolinux'
                      'manjaro-tools-base' 'mkinitcpio-nfs-utils'
                      'mktorrent' 'manjaro-iso-profiles-base')
	depends_x86_64=( ${depends_i686[@]} 'manjaro-efi-shell')
	optdepends=('manjaro-iso-profiles: Manjaro Linux ISO-Image profiles'
                    'manjaro-iso-profiles-community: Manjaro Linux ISO-Image community profiles'
                    'manjaro-iso-profiles-minimal: Manjaro Linux ISO-Image minimal profiles'
                    'sonar-iso-profiles: Sonar Linux ISO-Image profiles'
                    'qemu: quickly test isos')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}
