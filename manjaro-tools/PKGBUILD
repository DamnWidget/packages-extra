# Maintainer: artoo <artoo@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

pkgbase=manjaro-tools
pkgname=('manjaro-tools-base' 'manjaro-tools-pkg' 'manjaro-tools-iso')
pkgver=0.11.1
pkgrel=2
pkgdesc='Development tools for Manjaro Linux'
arch=('i686' 'x86_64')
license=('GPL')
groups=('manjaro-tools')
url='https://github.com/manjaro/manjaro-tools'
makedepends=('docbook2x')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
	"dm.patch::${url}/commit/8f362a29826e6ff895b16f29cc3d55c41fd07526.patch")
sha256sums=('c5f8ce509851aab528b9f4f5bc21a361507908dfbd4c9ced28dc42909b1e703d'
            '912b0c7f9c4d7488dab64d2d77d10a8d0e623649da6426b805e64a970e771953')

prepare() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	# patches here
	patch -Np1 -i $srcdir/dm.patch
}

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr
}

package_manjaro-tools-base() {
 	pkgdesc='Development tools for Manjaro Linux (base tools)'
	depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg')
	optdepends=('manjaro-tools-pkg: Manjaro Linux package tools'
                    'manjaro-tools-iso: Manjaro Linux iso tools')
	backup=('etc/manjaro-tools/manjaro-tools.conf')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_manjaro-tools-pkg() {
 	pkgdesc='Development tools for Manjaro Linux (packaging tools)'
	depends=('namcap' 'manjaro-tools-base')
	conflicts=('devtools')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_manjaro-tools-iso() {
 	pkgdesc='Development tools for Manjaro Linux (ISO tools)'
	depends_i686=('nbd' 'libisoburn' 'squashfs-tools' 'syslinux' 'isolinux'
                      'manjaro-tools-base' 'mkinitcpio-nfs-utils'
                      'mktorrent' 'manjaro-iso-profiles-base')
	depends_x86_64=( ${depends_i686[@]} 'manjaro-efi-shell')
	optdepends=('manjaro-iso-profiles: Manjaro Linux ISO-Image profiles'
                    'manjaro-iso-profiles-community: Manjaro Linux ISO-Image community profiles'
                    'manjaro-iso-profiles-minimal: Manjaro Linux ISO-Image minimal profiles'
                    'sonar-iso-profiles: Sonar Linux ISO-Image profiles'
                    'qemu: quickly test isos')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}
