# Maintainer: artoo <artoo@manjaro.org>

pkgbase=manjaro-tools
pkgname=('manjaro-tools-base' 'manjaro-tools-pkg' 'manjaro-tools-iso')
pkgver=0.9.8
pkgrel=3
pkgdesc='Development tools for Manjaro Linux'
arch=('any')
license=('GPL')
groups=('manjaro-tools')
url='https://github.com/manjaro/manjaro-tools'
makedepends=('docbook2x')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('767be1bced166b4ce96712cdab1257f9bbc10ba338550cdf5f3b6c4f29f978bb')

prepare() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	# patches here
}

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr
}

package_manjaro-tools-base() {
	depends=('openssh' 'rsync' 'haveged')
	optdepends=('manjaro-tools-pkg: Manjaro Linux package tools'
				'manjaro-tools-iso: Manjaro Linux iso tools')
	conflicts=('manjaro-tools')
	replaces=('manjaro-tools')
	backup=('etc/manjaro-tools/manjaro-tools.conf')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install

	install -dm755 $srcdir/_pkg/usr/bin
	install -dm755 $srcdir/_pkg/usr/lib/$pkgbase
	mv $pkgdir/usr/bin/{buildpkg,buildtree,mkchrootpkg,finddeps,lddd,find-libdeps,find-libprovides,checkpkg,signpkg,signpkgs} $srcdir/_pkg/usr/bin
	mv $pkgdir/usr/lib/$pkgbase/{util-pkg.sh,util-pkgtree.sh} $srcdir/_pkg/usr/lib/$pkgbase

	install -dm755 $srcdir/_iso/etc/$pkgbase/sets/iso
	install -dm755 $srcdir/_iso/usr/bin
	install -dm755 $srcdir/_iso/usr/lib/$pkgbase
	install -dm755 $srcdir/_iso/usr/share/$pkgbase
	mv $pkgdir/etc/$pkgbase/sets/iso/{openrc.set,official.set,community.set} $srcdir/_iso/etc/$pkgbase/sets/iso
	mv $pkgdir/usr/bin/{buildiso,testiso} $srcdir/_iso/usr/bin
	mv $pkgdir/usr/lib/$pkgbase/{util-iso*.sh,util-livecd.sh} $srcdir/_iso/usr/lib/$pkgbase
	mv $pkgdir/usr/lib/initcpio $srcdir/_iso/usr/lib/initcpio/
	mv $pkgdir/usr/share/$pkgbase/{scripts,efi_shell} $srcdir/_iso/usr/share/$pkgbase/
	mv $pkgdir/usr/share/$pkgbase/{pacman-lng.conf,pacman-gfx.conf} $srcdir/_iso/usr/share/$pkgbase
}

package_manjaro-tools-pkg() {
	depends=('namcap' 'manjaro-tools-base')
	conflicts=('devtools')

	mv "$srcdir/_pkg"/* "$pkgdir"
}

package_manjaro-tools-iso() {
	depends=('nbd' 'libisoburn' 'squashfs-tools' 'syslinux' 'manjaro-tools-base' 'mkinitcpio-nfs-utils')
	optdepends=('manjaro-iso-profiles: Manjaro Linux ISO-Image profiles'
				'manjaro-iso-profiles-community: Manjaro Linux ISO-Image community profiles'
				'manjaro-iso-profiles-openrc: Manjaro Linux ISO-Image openrc profiles'
				'qemu: quickly test isos')
	conflicts=('manjaroiso')

	mv "$srcdir/_iso"/* "$pkgdir"
}
