# Maintainer: artoo <artoo@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

pkgbase=manjaro-tools
pkgname=('manjaro-tools-base' 'manjaro-tools-pkg' 'manjaro-tools-iso')
pkgver=0.9.12
pkgrel=3
pkgdesc='Development tools for Manjaro Linux'
arch=('any')
license=('GPL')
groups=('manjaro-tools')
url='https://github.com/manjaro/manjaro-tools'
makedepends=('docbook2x')
_gitsource=ea57a2a4d785b2a3a820004f16a8e710311808f6
source=("${pkgbase}-${pkgver}-${pkgrel}.tar.gz::${url}/archive/${_gitsource}.tar.gz")
#source=("${pkgbase}-${pkgver}-${pkgrel}.tar.gz::${url}/archive/${pkgver}-${pkgrel}.tar.gz")
sha256sums=('e4429e055facb9f171cda3d89c9d988e43dab8de57fb6474f87cb3223a3308ae')

prepare() {
	if [ -e ${srcdir}/${pkgbase}-${_gitsource} ]; then
		mv ${srcdir}/${pkgbase}-${_gitsource} ${srcdir}/${pkgbase}-${pkgver}-${pkgrel}
	fi
	cd ${srcdir}/${pkgbase}-${pkgver}-${pkgrel}
	# patches here
}

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}-${pkgrel}
	make SYSCONFDIR=/etc PREFIX=/usr
}

package_manjaro-tools-base() {
	arch=('i686' 'x86_64')
 	pkgdesc='Development tools for Manjaro Linux (base tools)'
	depends_i686=('openssh' 'rsync' 'haveged' 'os-prober')
	depends_x86_64=('openssh' 'rsync' 'haveged' 'os-prober' 'manjaro-tools-efiboot')
	optdepends=('manjaro-tools-pkg: Manjaro Linux package tools'
				'manjaro-tools-iso: Manjaro Linux iso tools')
	conflicts=('manjaro-tools')
	replaces=('manjaro-tools')
	backup=('etc/manjaro-tools/manjaro-tools.conf')

	cd ${srcdir}/${pkgbase}-${pkgver}-${pkgrel}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_manjaro-tools-pkg() {
 	pkgdesc='Development tools for Manjaro Linux (packaging tools)'
	depends=('namcap' 'manjaro-tools-base')
	conflicts=('devtools')

	cd ${srcdir}/${pkgbase}-${pkgver}-${pkgrel}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_manjaro-tools-iso() {
 	pkgdesc='Development tools for Manjaro Linux (ISO tools)'
	depends=('nbd' 'libisoburn' 'squashfs-tools' 'syslinux' 'manjaro-tools-base' 'mkinitcpio-nfs-utils')
	optdepends=('manjaro-iso-profiles: Manjaro Linux ISO-Image profiles'
				'manjaro-iso-profiles-community: Manjaro Linux ISO-Image community profiles'
				'manjaro-iso-profiles-openrc: Manjaro Linux ISO-Image openrc profiles'
				'qemu: quickly test isos')
	conflicts=('manjaroiso')

	cd ${srcdir}/${pkgbase}-${pkgver}-${pkgrel}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}
