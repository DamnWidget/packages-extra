# Maintainer: artoo <artoo@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

pkgbase=manjaro-tools
pkgname=('manjaro-tools-base' 'manjaro-tools-pkg' 'manjaro-tools-iso')
pkgver=0.9.15
pkgrel=4
pkgdesc='Development tools for Manjaro Linux'
arch=('x86_64' 'i686')
license=('GPL')
groups=('manjaro-tools')
url='https://github.com/manjaro/manjaro-tools'
makedepends=('docbook2x')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        reset_profile_vars.patch::https://github.com/manjaro/manjaro-tools/commit/ff51d0e050730f514c304382ca815904538f30ae.patch
        reset_packages_vars.patch::https://github.com/manjaro/manjaro-tools/commit/f0e0b342bfaa5ed9f2e0bb6fb0c44e1b901e820f.patch
        budgie_v10.1.patch::https://github.com/manjaro/manjaro-tools/commit/97d0a31e516cd96fd8ccf4b2500387696c55ba41.patch
        update_docu.patch::https://github.com/manjaro/manjaro-tools/commit/89ffdb2dfc0d8704eae5e9a114f3b9251a9b2f96.patch)
md5sums=('80d1960796e76e5d3a79bdcc04e35244'
         '8eae0f84fd4da754b640971f10c97f19'
         '04fc23978b1f593bbbee39a4288663b7'
         '6f44c134256de70581deba98ead4dbf3'
         '65625a2ec35ede6b3e00fe5c0720c51a')

prepare() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	patch -Np1 -i $srcdir/reset_profile_vars.patch
	patch -Np1 -i $srcdir/reset_packages_vars.patch
	patch -Np1 -i $srcdir/budgie_v10.1.patch
	patch -Np1 -i $srcdir/update_docu.patch
}

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr
}

package_manjaro-tools-base() {
 	pkgdesc='Development tools for Manjaro Linux (base tools)'
	depends=('openssh' 'rsync' 'haveged' 'os-prober')
	optdepends=('manjaro-tools-pkg: Manjaro Linux package tools'
                    'manjaro-tools-iso: Manjaro Linux iso tools')
	backup=('etc/manjaro-tools/manjaro-tools.conf')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_manjaro-tools-pkg() {
 	pkgdesc='Development tools for Manjaro Linux (packaging tools)'
	depends=('namcap' 'manjaro-tools-base')
	conflicts=('devtools')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_manjaro-tools-iso() {
 	pkgdesc='Development tools for Manjaro Linux (ISO tools)'
	depends_i686=('nbd' 'libisoburn' 'squashfs-tools' 'syslinux'
                      'manjaro-tools-base' 'mkinitcpio-nfs-utils')
	depends_x86_64=( ${depends_i686[@]} 'manjaro-efi-shell' )
	optdepends=('manjaro-iso-profiles: Manjaro Linux ISO-Image profiles'
                    'manjaro-iso-profiles-community: Manjaro Linux ISO-Image community profiles'
                    'manjaro-iso-profiles-community-minimal: Manjaro Linux ISO-Image community minimal profiles'
                    'qemu: quickly test isos')

	cd ${srcdir}/${pkgbase}-${pkgver}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}
