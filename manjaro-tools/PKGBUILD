# Maintainer: artoo <artoo@manjaro.org>
# Maintainer: Philip MÃ¼ller <philm@manjaro.org>

pkgbase=manjaro-tools
pkgname=('manjaro-tools-base' 'manjaro-tools-pkg' 'manjaro-tools-iso')
_tarball=eca858eb04da08198a7b22b758eedbe58cd5b396
pkgver=0.11.901
pkgrel=2
pkgdesc='Development tools for Manjaro Linux'
arch=('i686' 'x86_64')
license=('GPL')
groups=('manjaro-tools')
url='https://github.com/manjaro/manjaro-tools'
makedepends=('docbook2x')
source=("${pkgbase}-${_tarball}.tar.gz::${url}/archive/${_tarball}.tar.gz"
        "devtools-backport.patch::https://github.com/manjaro/manjaro-tools/commit/e44826a2e14bc2e55d3c462a8de05329d161a9d2.patch"
        "fix-mounts.patch::https://github.com/manjaro/manjaro-tools/commit/b2a946b34d93947a2deb9d177290c0d63f1bcbec.patch"
        "gpt-auto-generator.patch::https://github.com/manjaro/manjaro-tools/commit/e6625187a1a5fe31e99d3e448ce2175f4f594402.patch")
sha256sums=('c5e5fe508a8c371a749793a7937bacc0ecbf4039282d13136616f4776e50273f'
            'be7fc53230b17e32b64f7cbc58dbf2c265691a2fcdc7435f808986ebb0a40ac1'
            'f3c81b382e3a69a2529b77479fd99586e4ce97d691e181aa643b239cbffde5a1'
            '6fcd5079aeeba2fb4be009afb30624af4a2011f4b8a67ef69c7293079c082b96')

prepare() {
	cd ${srcdir}/${pkgbase}-${_tarball}
	# patches here
	patch -p1 -i "$srcdir/devtools-backport.patch"
	patch -p1 -i "$srcdir/fix-mounts.patch"
	patch -p1 -i "$srcdir/gpt-auto-generator.patch"
}

build() {
	cd ${srcdir}/${pkgbase}-${_tarball}
	make SYSCONFDIR=/etc PREFIX=/usr
}

package_manjaro-tools-base() {
 	pkgdesc='Development tools for Manjaro Linux (base tools)'
	depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman-mirrorlist>=20160301')
	optdepends=('manjaro-tools-pkg: Manjaro Linux package tools'
                    'manjaro-tools-iso: Manjaro Linux iso tools')
	backup=('etc/manjaro-tools/manjaro-tools.conf')

	cd ${srcdir}/${pkgbase}-${_tarball}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_manjaro-tools-pkg() {
 	pkgdesc='Development tools for Manjaro Linux (packaging tools)'
	depends=('namcap' 'manjaro-tools-base')
	conflicts=('devtools')

	cd ${srcdir}/${pkgbase}-${_tarball}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_manjaro-tools-iso() {
 	pkgdesc='Development tools for Manjaro Linux (ISO tools)'
	depends_i686=('nbd' 'libisoburn' 'squashfs-tools' 'syslinux' 'isolinux'
                      'manjaro-tools-base' 'mkinitcpio-nfs-utils'
                      'mktorrent' 'manjaro-iso-profiles-base')
	depends_x86_64=( ${depends_i686[@]} 'manjaro-efi-shell')
	optdepends=('manjaro-iso-profiles: Manjaro Linux ISO-Image profiles'
                    'manjaro-iso-profiles-community: Manjaro Linux ISO-Image community profiles'
                    'manjaro-iso-profiles-minimal: Manjaro Linux ISO-Image minimal profiles'
                    'sonar-iso-profiles: Sonar Linux ISO-Image profiles'
                    'qemu: quickly test isos')

	cd ${srcdir}/${pkgbase}-${_tarball}
	make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}
