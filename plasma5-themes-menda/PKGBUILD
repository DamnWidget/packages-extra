# $Id$
# Maintainer : Ramon Buld√≥ ramon@manjaro.org
# Contributor: anex <assassin.anex[@]gmail.com>

pkgbase=plasma5-themes-menda
pkgname=('plasma5-themes-menda' 'sddm-menda-theme')
pkgver=20150304
pkgrel=2
arch=('any')
url='https://github.com/manjaro/artwork-menda-plasma5'
license=('LGPL')
makedepends=('extra-cmake-modules' 'plasma-framework' 'git')
source=("git+https://github.com/manjaro/artwork-menda-plasma5.git")
sha256sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../artwork-menda-plasma5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package_plasma5-themes-menda() {
  pkgdesc='Artwork, styles and assets for the Menda visual style for the Plasma 5 Desktop'
  depends=('breeze' 'breeze-kde4' 'menda-circle-icon-theme' 'xcursor-menda')
  optdepends=('menda-themes: Menda theme for Gtk2, Gtk3, Metacity, Xfwm, Openbox, Cinnamon and GNOME Shell'
              'menda-themes-dark: Dark Menda theme for Gtk2, Gtk3, Metacity, Xfwm, Openbox, Cinnamon and GNOME Shell')
  replaces=('menda' 'menda-kde4' 'menda-plasma-desktop' 'menda-plasma-look-and-feel')
  cd build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share/sddm"
}

package_sddm-menda-theme() {
  pkgdesc="Menda Theme for SDDM"
  cd build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share/color-schemes"
  rm -rf "${pkgdir}/usr/share/kservices5"
  rm -rf "${pkgdir}/usr/share/plasma"
  rm -rf "${pkgdir}/usr/share/wallpapers"
} 
