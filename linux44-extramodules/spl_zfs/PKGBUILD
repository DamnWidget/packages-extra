# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgbase=linux44-spl_zfs
pkgname=('linux44-spl' 'linux44-zfs')
pkgver=0.6.5.2
pkgrel=0.2
url="http://zfsonlinux.org/"
arch=('i686' 'x86_64')
license=("CDDL")
depends=('linux44')
makedepends=("spl-dkms>=$pkgver" "zfs-dkms>=$pkgver"
             'dkms'
             'linux44' 'linux44-headers')
groups=('linux44-extramodules')
install=install

source=(4.4-support-patch::https://github.com/zfsonlinux/zfs/files/41928/4.4-support-patch.txt)
sha256sums=('f60e21f52a3caff63935c185b7a74b6afb29be52432aebfc4e3a5d935399f47b')

prepare() {
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf

  # workaround to patch for linux 4.4
  rm -r $srcdir/dkms/zfs/$pkgver/source
  cp -r /usr/src/zfs-$pkgver $srcdir/dkms/zfs/$pkgver/source
  cd dkms/zfs/$pkgver/source
  patch -Np1 -i $srcdir/4.4-support-patch
}

build() {
  _extraver=extramodules-4.4-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"

  # build host modules
  msg2 'Build modules'
  dkms --dkmsframework dkms.conf build "spl/$pkgver" -k "$_kernver" --verbose
  dkms --dkmsframework dkms.conf build "zfs/$pkgver" -k "$_kernver" --verbose
}

package_linux44-zfs(){
  _extraver=extramodules-4.4-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"
  _pkgname=zfs
  pkgdesc='Kernel modules for the Zettabyte File System.'
  provides=("$_pkgname=$pkgver")
  depends=('linux44' "zfs-utils=${pkgver}")

  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/zfs/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/install"
}

package_linux44-spl(){
  _extraver=extramodules-4.4-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"
  _pkgname=spl
  pkgdesc='Solaris Porting Layer kernel modules.'
  provides=("$_pkgname=$pkgver")
  depends=('linux44' "spl-utils=${pkgver}")

  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/spl/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/install"
}
