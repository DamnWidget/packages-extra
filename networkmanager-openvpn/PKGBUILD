# $Id: PKGBUILD 251899 2015-11-23 18:29:31Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-openvpn
pkgver=1.0.9
pkgrel=1
_gitsnap=e547321c9a472f9e6cacae97ddbb35957068bdd7
pkgdesc="NetworkManager VPN plugin for OpenVPN"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=("libnm-gtk" 'openvpn' 'gtk3' 'libsecret')
makedepends=('intltool' 'autogen')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
install=networkmanager-openvpn.install
source=(#http://ftp.gnome.org/pub/GNOME/sources/network-manager-openvpn/${pkgver::3}/network-manager-openvpn-${_gitsnap}.tar.xz
        https://git.gnome.org/browse/network-manager-openvpn/snapshot/network-manager-openvpn-${_gitsnap}.tar.xz)
sha256sums=('bf30d2e4747149cd8d11163d3a8a5d694f2a4ab8b594057a9755247f26d8835a')

prepare() {
  cd network-manager-openvpn-${_gitsnap}
}

build() {
  cd network-manager-openvpn-${_gitsnap}
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --enable-more-warnings=yes \
    --disable-static
  make
}

package() {
  cd network-manager-openvpn-${_gitsnap}
  make DESTDIR="${pkgdir}" install
}
