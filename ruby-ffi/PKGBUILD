# Maintainer: artoo <artoo@manjaro.org>

_gemname=ffi
pkgname=ruby-${_gemname}
pkgver=1.9.13
pkgrel=1
pkgdesc='Ruby FFI'
arch=('i686' 'x86_64')
url='http://github.com/ffi/ffi'
license=('BSD')
depends=('ruby' 'libffi')
options=('!emptydirs')
noextract=("${_gemname}-${pkgver}.gem")
source=("https://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
sha256sums=('6b0e2c6d25188e79e7b3c5f972d7d4ee234eb008d2f1efbc10fd6d29c3e55478')

package() {
		local _gemdir="$(ruby -e'puts Gem.default_dir')"
		gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem

		rm -rf "${pkgdir}/${_gemdir}/cache"

		rm -rf "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/ext"

		install -D -m644 "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

		install -D -m644 "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
