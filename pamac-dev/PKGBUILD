# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgname=pamac-dev
_pkgname=pamac
pkgver=4.0beta3
_pkgver=0105b98f9ee6ff36b5560b0b9a30926ea0452bcc
pkgrel=1
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/guinux/pamac2"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38' 'gtk3>=3.18'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'pacman<5.1' 'gnutls>=3.4')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
            'lxsession: needed for authentification in Xfce, LXDE etc.'
            'yaourt: needed for AUR support')
makedepends=('gettext' 'itstool' 'vala>=0.28')
backup=('etc/pamac.conf')
replaces=('pamac-aur')
conflicts=("$_pkgname")
options=(!emptydirs)
install=$_pkgname.install

source=("pamac-$pkgver.tar.xz::https://github.com/manjaro/pamac/archive/$_pkgver.tar.gz"
        "headerbar-$pkgver.patch::https://github.com/manjaro/pamac/pull/114.patch")
sha256sums=('a75c5e1ffa728576eff9994f7a8fdb477df7e07230e65bb7b3a300d6ccce199b'
            'b51610c4fb1aa204214fde6e0d11f9dc4aa66d0fa9b56b29d98a8cc449df0218')
  
prepare() {
  # adjust version string
  cd "$srcdir/pamac-$_pkgver/src"
  sed -i -e "s|\"4.0.0\"|\"$pkgver-$pkgrel\"|g" manager_window.vala 
  cd "$srcdir/pamac-$_pkgver/"
  # patches here
  patch -p1 -i "$srcdir/headerbar-$pkgver.patch"
}

build() {
  cd "$srcdir/pamac-$_pkgver"

  # build
  make all
}

package() {
  cd "$srcdir/pamac-$_pkgver"
  make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install
}

# vim:set ts=2 sw=2 et:
