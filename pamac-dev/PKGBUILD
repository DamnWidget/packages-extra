# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgname=pamac-dev
_pkgname=pamac
pkgver=4.0beta4
_pkgver=f0ac44bc4fd1080e04bee24479f213a9b152b351
pkgrel=1
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/guinux/pamac2"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38' 'gtk3>=3.18'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'pacman<5.1' 'gnutls>=3.4')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
            'lxsession: needed for authentification in Xfce, LXDE etc.'
            'yaourt: needed for AUR support')
makedepends=('gettext' 'itstool' 'vala>=0.28')
backup=('etc/pamac.conf')
replaces=('pamac-aur')
conflicts=("$_pkgname")
options=(!emptydirs)
install=$_pkgname.install

source=("pamac-$pkgver.tar.xz::https://github.com/manjaro/pamac/archive/$_pkgver.tar.gz")
sha256sums=('5241df29635561bca5c3365697285fb05381d76640f7d48516ccdbe5123d16eb')
  
prepare() {
  # adjust version string
  cd "$srcdir/pamac-$_pkgver"
  sed -i -e "s|\"4.0.0\"|\"$pkgver-$pkgrel\"|g" src/transaction.vala 
  # patches here
}

build() {
  cd "$srcdir/pamac-$_pkgver"

  # build
  make all
}

package() {
  cd "$srcdir/pamac-$_pkgver"
  make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install
}

# vim:set ts=2 sw=2 et:
