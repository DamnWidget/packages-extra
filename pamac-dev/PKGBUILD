# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgbase=pamac
pkgname=('pamac-dev' 'pamac-dev-tray-appindicator')
pkgver=4.1.5
_pkgver=e4b6528b8685a41914fb0c568c57bcc319459257
pkgrel=1
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38' 'gtk3>=3.18'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'pacman<5.1' 'gnutls>=3.4')
makedepends=('gettext' 'itstool' 'vala>=0.28' 'libappindicator-gtk3')
options=(!emptydirs)

source=("pamac-$pkgver-$pkgrel.tar.gz::https://github.com/manjaro/pamac/archive/$_pkgver.tar.gz")
sha256sums=('3d6f298de06b86e68d951dbef6e2416a246196e57a222dc027a0eee8e9fb9f23')
  
prepare() {
  # adjust version string
  cd "$srcdir/pamac-$_pkgver"
  sed -i -e "s|\"4.1.5\"|\"$pkgver-$pkgrel\"|g" src/transaction.vala
  # patches here
}

build() {
  cd "$srcdir/pamac-$_pkgver"

  # build
  make all use_appindicator=true
}

package_pamac-dev() {
  optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
              'lxsession: needed for authentification in Xfce, LXDE etc.'
              'yaourt: needed for AUR support'
              'pamac-dev-tray-appindicator: tray icon for KDE')
  backup=('etc/pamac.conf')
  conflicts=('pamac')
  replaces=('pamac-aur')
  install=pamac.install
  cd "$srcdir/pamac-$_pkgver"
  make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install
}

package_pamac-dev-tray-appindicator() {
  pkgdesc="Tray icon using appindicator which feets better in KDE"
  depends=('pamac' 'libappindicator-gtk3')
  conflicts=('pamac-tray-appindicator')
  cd "$srcdir/pamac-$_pkgver"
  make use_appindicator=true
  install -Dm755 src/pamac-tray-appindicator "$pkgdir"/usr/bin/pamac-tray-appindicator
  install -Dm644 data/applications/pamac-tray-appindicator.desktop "$pkgdir"/etc/xdg/autostart/pamac-tray-appindicator.desktop
}
# vim:set ts=2 sw=2 et:
