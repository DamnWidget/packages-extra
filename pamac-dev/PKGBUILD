# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgname=pamac-dev
_pkgname=pamac
pkgver=4.1.3
_pkgver=9f3127615feb72e1d84d76ba54eea5bbca2ebd2f
pkgrel=2
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38' 'gtk3>=3.18'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'pacman<5.1' 'gnutls>=3.4'
         'libappindicator-gtk3')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
            'lxsession: needed for authentification in Xfce, LXDE etc.'
            'yaourt: needed for AUR support')
makedepends=('gettext' 'itstool' 'vala>=0.28' 'libappindicator-gtk3')
backup=('etc/pamac.conf')
replaces=('pamac-aur')
conflicts=("$_pkgname")
options=(!emptydirs)
install=$_pkgname.install

source=("pamac-$pkgver-$pkgrel.tar.xz::https://github.com/manjaro/pamac/archive/$_pkgver.tar.gz")
sha256sums=('a6dd6dc74595938fdf1e2ab82b61c9f594295149dd7c8c0267186f3c5a17f49b')
  
prepare() {
  # adjust version string
  cd "$srcdir/pamac-$_pkgver"
  sed -i -e "s|\"4.1.2\"|\"$pkgver-$pkgrel\"|g" src/transaction.vala 
  # patches here
}

build() {
  cd "$srcdir/pamac-$_pkgver"

  # build
  make all
}

package() {
  cd "$srcdir/pamac-$_pkgver"
  make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install
}

# vim:set ts=2 sw=2 et:
