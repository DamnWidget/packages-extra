# $Id: PKGBUILD 249755 2015-10-25 18:39:57Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=polkit
pkgver=0.113
pkgrel=4.2
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(i686 x86_64)
license=(LGPL)
url="http://www.freedesktop.org/wiki/Software/polkit"
depends=(glib2 pam expat systemd js17)
makedepends=(intltool gtk-doc gobject-introspection git)
install=polkit.install
source=(http://www.freedesktop.org/software/polkit/releases/$pkgname-$pkgver.tar.gz x11vars.patch)
md5sums=('4b77776c9e4f897dcfe03b2c34198edf'
         '6483b6cbde24d0b3eb3ef137db06e617')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i $srcdir/x11vars.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/polkit-1 \
      --enable-libsystemd-login=yes --disable-static \
      --enable-gtk-doc --with-os-type=redhat
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  chown root:102 "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
  chmod 750      "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
}
