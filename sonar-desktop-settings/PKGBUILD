# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

pkgbase=desktop-settings
pkgname=('sonar-gnome-settings'
		'sonar-mate-settings'
		'sonar-mate-panel-layout')
pkgver=20160823
pkgrel=3
arch=('any')
url="https://github.com/manjaro/desktop-settings"
license=('GPL')
depends=('manjaro-base-skel')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')

_inst_dir(){
	if [ -d $srcdir/$pkgbase/$1/skel ]; then
		install -d $pkgdir/etc
		cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
	fi

	if [[ -d $srcdir/$pkgbase/$1/scripts ]]; then
		install -d $pkgdir/usr/bin
		cp $srcdir/$pkgbase/$1/scripts/* $pkgdir/usr/bin
	fi

	if [[ -d $srcdir/$pkgbase/$1/schemas ]]; then
		install -d $pkgdir/usr/share/glib-2.0/schemas
		cp $srcdir/$pkgbase/$1/schemas/* $pkgdir/usr/share/glib-2.0/schemas
	fi
}

pkgver() {
	date +%Y%m%d
}

package_sonar-mate-settings() {
	pkgdesc='Sonar Linux mate settings'
	conflicts=('sonar-desktop-settings')
	provides=('sonar-desktop-settings')

	_inst_dir 'sonar/mate'
}

package_sonar-gnome-settings() {
	pkgdesc='Sonar Linux gnome settings'
	conflicts=('sonar-desktop-settings')
	provides=('sonar-desktop-settings')

	_inst_dir 'sonar/gnome'
}

package_sonar-mate-panel-layout() {
	pkgdesc='Sonar Linux mate-gtk3 panel layout'
	depends=('mate-panel-gtk3')

	install -Dm 755 $srcdir/$pkgbase/sonar/mate/layouts/sonar.layout $pkgdir/usr/share/mate-panel/layouts/sonar.layout
}
