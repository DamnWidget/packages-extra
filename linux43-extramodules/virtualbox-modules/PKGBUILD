# Based on the file created for Arch Linux by:
# Ionut Biru <ibiru@archlinux.org>
# Sébastien Luttringer <seblu@aur.archlinux.org>

# Maintainer: Philip Müller <philm@manjaro.org>
# Maintainer: Guinux <guillaume@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>


pkgbase=linux43-virtualbox-modules
pkgname=('linux43-virtualbox-host-modules' 'linux43-virtualbox-guest-modules')
pkgver=5.0.4
pkgrel=0.5
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=("virtualbox-host-dkms>=$pkgver"
             "virtualbox-guest-dkms>=$pkgver"
             'dkms'
             'linux43' 'linux43-headers')

_extraver=extramodules-4.3-MANJARO
_kernver="$(cat /usr/lib/modules/${_extraver}/version)"

source=('SUPDrv-linux.patch::https://aur.archlinux.org/cgit/aur.git/plain/SUPDrv-linux.patch?h=virtualbox-modules-mainline'
        'VBoxNetAdp-linux.patch::https://aur.archlinux.org/cgit/aur.git/plain/VBoxNetAdp-linux.patch?h=virtualbox-modules-mainline')

sha256sums=('479c3ed07fa82b8bdabfa8b4329bc5f3f692cce2303fc8cc3fe82549f21d3a58'
            '195500efaee9cfbfedfdfc16e7b376e69d012521bc0256c2049a4215b2dc7ec8')

prepare() {
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf

  # copy this dir to local dir so we can patch it.
  cp -r -L dkms/vboxhost/$pkgver/source dkms/vboxhost/$pkgver/src
  rm dkms/vboxhost/$pkgver/source
  mv dkms/vboxhost/$pkgver/src dkms/vboxhost/$pkgver/source

  patch dkms/vboxhost/$pkgver/source/vboxdrv/linux/SUPDrv-linux.c < $srcdir/SUPDrv-linux.patch
  patch dkms/vboxhost/$pkgver/source/vboxnetadp/linux/VBoxNetAdp-linux.c < $srcdir/VBoxNetAdp-linux.patch
}

build() {
  # build host modules
  msg2 'Host modules'
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
  # build guest modules  
  msg2 'Guest modules'
  dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_linux43-virtualbox-host-modules(){
  pkgdesc='Host kernel modules for VirtualBox'
  depends=('linux43')
  replaces=('linux43-virtualbox-modules')
  conflicts=('linux43-virtualbox-modules')
  provides=('virtualbox-host-modules')
  install=virtualbox-host-modules.install
  groups=('linux43-extramodules')


  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/virtualbox-host-modules.install"

  mkdir -p "$pkgdir/etc/modules-load.d"
  echo "vboxdrv" > "$pkgdir/etc/modules-load.d/linux43-virtualbox-host-modules.conf"
  echo "vboxnetadp" >> "$pkgdir/etc/modules-load.d/linux43-virtualbox-host-modules.conf"
  echo "vboxnetflt" >> "$pkgdir/etc/modules-load.d/linux43-virtualbox-host-modules.conf"
  echo "vboxpci" >> "$pkgdir/etc/modules-load.d/linux43-virtualbox-host-modules.conf"
}

package_linux43-virtualbox-guest-modules(){
  pkgdesc='Guest kernel modules for VirtualBox'
  license=('GPL')
  depends=('linux43')
  replaces=('linux43-virtualbox-manjaro-modules')
  conflicts=('linux43-virtualbox-manjaro-modules')
  provides=('virtualbox-guest-modules')
  install=virtualbox-guest-modules.install
  groups=('linux43-extramodules')


  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/virtualbox-guest-modules.install"

  mkdir -p "$pkgdir/etc/modules-load.d"
  echo "vboxguest" > "$pkgdir/etc/modules-load.d/linux43-virtualbox-guest-modules.conf"
  echo "vboxsf" >> "$pkgdir/etc/modules-load.d/linux43-virtualbox-guest-modules.conf"
  echo "vboxvideo" >> "$pkgdir/etc/modules-load.d/linux43-virtualbox-guest-modules.conf"
}

# vim:set ts=2 sw=2 et:
