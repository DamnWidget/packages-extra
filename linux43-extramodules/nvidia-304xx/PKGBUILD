# Based on the file created for Arch Linux by:
# Maintainer : Thomas Baechler <thomas@archlinux.org>

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

_linuxprefix=linux43
_extramodules=extramodules-4.3-MANJARO
pkgname=$_linuxprefix-nvidia-304xx
_pkgname=nvidia
pkgver=304.128
pkgrel=0.4
pkgdesc="NVIDIA drivers for linux."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=("$_linuxprefix" "nvidia-304xx-utils=${pkgver}")
makedepends=("$_linuxprefix-headers")
provides=("$_pkgname=$pkgver" "$_linuxprefix-nvidia-legacy")
replaces=("$_linuxprefix-nvidia-legacy")
groups=("$_linuxprefix-extramodules")
conflicts=('nvidia-96xx' 'nvidia-173xx' "$_linuxprefix-nvidia")
license=('custom')
install=nvidia.install
options=(!strip)
source=('nvidia-3.18.patch' 'nvidia-4.3.patch')
sha256sums=('c9986c306f452614fcf23990c55ffe12bdc451bcbd65a5200269f90a722a3d35'
            '0e11d4d73556e39fffce5b8c14674ede0064cabee058d9fb8f8bd7c3035fca5d')

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source+=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    sha256sums+=('f6aa695b3375473e3444412e4a3b7ef6bd8b13e3f3678f4cbe7a3649b5129068')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source+=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    sha256sums+=('f54bf11d1d315035fed1214233a92b97918ed3dabbb3fe8489bf8651f6d90ca1')
fi

prepare() {
  cd "$srcdir"
  sh "$_pkg.run" --extract-only
  cd "${_pkg}"
  # patches here
  # https://devtalk.nvidia.com/default/topic/783364/343-22-driver-incompatible-with-linux-3-18-/
  patch -Np0 -i ${srcdir}/nvidia-3.18.patch
  patch -Np1 -i ${srcdir}/nvidia-4.3.patch
}

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
