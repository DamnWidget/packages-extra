# $Id: PKGBUILD 248535 2015-10-08 19:49:55Z heftig $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=cogl
pkgver=1.22.2
pkgrel=1.1
pkgdesc="An object oriented GL/GLES Abstraction/Utility Layer"
arch=('i686' 'x86_64')
url="http://www.clutter-project.org/"
license=('GPL2')
depends=('mesa' 'libdrm' 'libxext' 'libxdamage' 'libxcomposite' 'gdk-pixbuf2' 'pango' 'libxrandr')
makedepends=('gobject-introspection')
options=(!emptydirs)
replaces=('cogl-catalyst')
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        gl_Add_a_quirk_for_the_GLSL_version_to_use_on_AMD_v2.patch
        Use_better_vendor_strings_from_AMD.patch)
sha256sums=('39a718cdb64ea45225a7e94f88dddec1869ab37a21b339ad058a9d898782c00d'
            'bed12e762657b2a864fd3abe8722e8acf436576d8fd1e7325d69590294ac72b0'
            '47a2ed89b5c6c1e3a6e5b64c97ae4dc000290b6bc9ef18b276934de4d29e8933')

prepare() {
  cd $pkgname-$pkgver
  # https://bugzilla.gnome.org/show_bug.cgi?id=756306
  patch -Np1 -i ../gl_Add_a_quirk_for_the_GLSL_version_to_use_on_AMD_v2.patch
  patch -Np1 -i ../Use_better_vendor_strings_from_AMD.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-gles{1,2} \
    --enable-{kms,wayland}-egl-platform \
    --enable-wayland-egl-server

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make -j1
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
