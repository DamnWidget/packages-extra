# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=("zfs-dkms")
pkgver=0.6.5.6
pkgrel=2
pkgdesc="Kernel modules for the Zettabyte File System."
depends=("spl-dkms=${pkgver}" "zfs-utils=${pkgver}" "dkms")
arch=("any")
url="http://zfsonlinux.org/"
source=(http://archive.zfsonlinux.org/downloads/zfsonlinux/zfs/zfs-${pkgver}.tar.gz
        page_cache.patch::https://github.com/zfsonlinux/zfs/commit/8b1899d3fbab354dfe57b2293216a76579e5d805.patch)
groups=("manjarozfs")
sha256sums=('c349d46d86b4f61cd53a0891acad916cfc3f0d6754127db7f60a0bd98185aeff'
            '2304a5add101dee05a51900b2e986f4ae21c5c5ac3a837a7d3a9e42c1695c5c8')
license=("CDDL")
install=zfs-dkms.install

prepare() {
 cd "$srcdir/zfs-${pkgver}"
 patch -Np1 -i $srcdir/page_cache.patch
}

build() {
  cd "$srcdir/zfs-${pkgver}"
  ./autogen.sh
  scripts/dkms.mkconf -v ${pkgver} -f dkms.conf -n zfs
}


package() {
   install -d ${pkgdir}/usr/src/zfs-${pkgver}
   cp -a ${srcdir}/zfs-${pkgver}/ ${pkgdir}/usr/src/
   # fix dkms.conf
   sed -i 's|${kernelver}/${arch}|build|g' ${pkgdir}/usr/src/zfs-${pkgver}/dkms.conf
}
