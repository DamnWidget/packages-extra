# $Id: PKGBUILD 197969 2013-10-30 11:59:30Z allan $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=garcon
pkgver=0.3.0
pkgrel=4
pkgdesc="Implementation of the freedesktop.org menu specification"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('LGPL')
groups=('xfce4')
depends=('glib2' 'gtk2' 'libxfce4ui' 'libxfce4util')
makedepends=('pkgconfig' 'intltool' 'xfce4-dev-tools')
replaces=('libxfce4menu')
options=('!makeflags' '!libtool')
source=(http://archive.xfce.org/src/xfce/garcon/0.3/garcon-0.3.0.tar.bz2
        icons.patch::http://bug-attachment.xfce.org/attachment.cgi?id=5395)
sha256sums=('59348179f3cb9589d13ba8d18d5746be421b311cab974ce057dfdef0365b1e55'
            'f34b759fd015f311fc575f393da6b377eafe70f0a0119a1bb5c9c1d81ff86b42')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Patch for some icons not being displayed in xfce4-panel
  # https://bugzilla.xfce.org/show_bug.cgi?id=10709
  patch -p1 < $srcdir/icons.patch

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
