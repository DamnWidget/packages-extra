# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kompare
pkgver=15.04.2
pkgrel=1.1
pkgdesc='Diff/Patch Frontend'
url='http://kde.org/applications/development/kompare/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('libkomparediff2' 'ktexteditor' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdesdk-kompare')
replaces=('kdesdk-kompare')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kompare-${pkgver}.tar.xz" 
        "0001-Fix-only-show-directories-in-compare-files-dialog.patch"
        "save-preferences.patch")
sha1sums=('4108d51a4644f9f9e935070cab309a19f7dddcd4'
          '75719cc39852605d929e71aea4eef8ba506c2a4e'
          '8c4271c98bfb200c1630c37167aa410a775dcbf2')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  
  # Fix launching from the .desktop file
  patch -p1 -i $srcdir/0001-Fix-only-show-directories-in-compare-files-dialog.patch
  # Fix saving preferences https://bugs.kde.org/show_bug.cgi?id=346249
  patch -p1 -i "$srcdir"/save-preferences.patch
}

build() {
  cd build
  cmake ../kompare-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
