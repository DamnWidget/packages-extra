# Maintainer:  Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer:  Roland Singer <roland[at]manjaro[dog]org>
# Contributors: twa022 <twa022 at gmail dot com>
# Contributors: Pablo Lezaeta <prflr88 (arroba) gmail (dot) com>

pkgname=mdm
pkgver=2.0.10
pkgrel=1
_git=90dd4b6009dc0cdad4b324eded972f2c4e57eb81
pkgdesc="Linux Mint display manager, a fork of GDM 2.20.x"
arch=('i686' 'x86_64')
url="http://www.linuxmint.com"
license=('GPL')
groups=()
depends=('pam' 'libdmx' 'gtk2' 'libgnomecanvas' 'librsvg' 'tcp_wrappers'
         'libxml2' 'libart-lgpl' 'dbus-glib' 'libwebkit' 'polkit')
makedepends=('gnome-common' 'intltool' 'perl' 'gnome-doc-utils' 'xorg-server' 'pango' 'zenity' 'popt')
optdepends=('webkitgtk: mdm-theme-emulator' 'xorg-server-xnest: mdmthemetester')
provides=('gdm' 'gdm-old' "mintdm=$pkgver")
conflicts=('gdm' 'mintdm' 'mdm-display-manager')
replaces=('mintdm')
backup=('etc/mdm/custom.conf' 'etc/pam.d/mdm' 'etc/pam.d/mdm-autologin')
options=('!libtool')
install='mdm.install'
changelog=
source=("$pkgname-$pkgver.zip::https://github.com/linuxmint/$pkgname/archive/$pkgver.zip"
        #"$pkgname-$pkgver-$pkgrel.zip::https://github.com/linuxmint/$pkgname/archive/$_git.zip"
	'mdm.pam'
	'mdm.service'
	'mdm-autologin.pam'
	'defaults.conf'
	'mdm-plymouth.service'
        'org.cinnamon.pkexec.mdmsetup.policy')
sha256sums=('217af75ab555a17497e55c5b6ad6899f685de48592fb59bea2f14e77aeb574dd'
            'dda153bd02aed1e27b3ddc729130907e185e270e7b2b8d0ccda5bc904d47ea6b'
            '110b793011efd8b8bad4869f4726367146c381147fdeb578d2768a4bf749051c'
            '3db920fb26b10a22dd8c4ebaf1de1d1e83de613be61d8bd5206b8eb9ffedc2d7'
            '01cac1714eb82bfda9260e83e5e9728eda3966c7653ce2ead9d38a9ccda26855'
            '4f71596d0c91760d5e425014bc8fdff0fbdc41d0ea43e020220cb6a2bc366bd7'
            'bd22d536a7a1b455a8f8a45d7d8cc46156cb4ef5f3bc706dd73c89d503acad6b')

noextract=()

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}
	#cd ${srcdir}/${pkgname}-${_git}
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	#cd ${srcdir}/${pkgname}-${_git}
	gnome-doc-prepare --force
	chmod +x ./autogen.sh
	export AUTOMAKE=automake
	./autogen.sh --enable-ipv6=yes --with-prefetch \
	--prefix=/usr --with-console-kit=no --sysconfdir=/etc \
	--with-systemd --libexecdir=/usr/lib/mdm --localstatedir=/var/lib \
	--disable-static --with-xevie=yes --disable-scrollkeeper \
	--enable-secureremote=yes --enable-ipv6=yes \
	--enable-compile-warnings=no --sbindir=/usr/bin \
	LDFLAGS="-lXau -lm"
	sed -i -e 's|${prefix}|/usr|' config.h
	make DESTDIR=$pkgdir
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	#cd ${srcdir}/${pkgname}-${_git}
	make DESTDIR=$pkgdir install

msg2 'Adding PAM rules'
	#PAM, we use our own, not LinuxMint stuff, problem?...
	install -m755 -d $pkgdir/etc/pam.d
	install -m644 $srcdir/mdm-autologin.pam $pkgdir/etc/pam.d/mdm-autologin
	install -m644 $srcdir/mdm.pam $pkgdir/etc/pam.d/mdm

msg2 'Adding SystemD services'
	#systemd Init script, but ... SysV Inint is soo old
	install -m755 -d $pkgdir/usr/lib/systemd/system
	install -m644 $srcdir/mdm.service $pkgdir/usr/lib/systemd/system/mdm.service
	install -m644 $srcdir/mdm-plymouth.service $pkgdir/usr/lib/systemd/system/mdm-plymouth.service

msg2 'Adding config files'
	#configuration
	install -m444 "${srcdir}/defaults.conf" "${pkgdir}/usr/share/mdm/"
	install -m444 "${srcdir}/defaults.conf" "${pkgdir}/usr/share/mdm/distro.conf"
	rm -f ${pkgdir}/usr/share/xsessions/gnome.desktop || true

msg2 'Adding PolicyKit execution scheme'
	#PolicyKit execution scheme (need some tests)
	install -m755 -d $pkgdir/usr/share/polkit-1/actions/
	install -m644 $srcdir/org.cinnamon.pkexec.mdmsetup.policy $pkgdir/usr/share/polkit-1/actions/

	#Why on the hay this directory is created empty in etc, remove it 
	rmdir $pkgdir/etc/dm

msg2 'Adding faces'
	#Fix mdm files conflict with gnome-control-center (usr/share/pixmaps/faces/*)
	install -m755 -d $pkgdir/usr/share/pixmaps/faces/

	make DESTDIR=$pkgdir install -C gui/faces

msg2 'Adding applications'
	#Fix gdmsetup.desktop
	sed -i -e 's|^Exec=gksu|Exec=pkexec|' $pkgdir/usr/share/mdm/applications/mdmsetup.desktop
	sed -i -e 's|^Categories=|Categories=GNOME;GTK;System;Settings;X-GNOME-Settings-Panel;X-GNOME-SystemSettings;X-XFCE-SettingsDialog;X-XFCE-SystemSettings;|' $pkgdir/usr/share/mdm/applications/mdmsetup.desktop
	
	#Fix erroneous path for certain applications
	install -m755 -d $pkgdir/usr/share/applications
	mv -f $pkgdir/usr/share/mdm/applications/*.* $pkgdir/usr/share/applications
	chmod 755 $pkgdir/usr/share/applications/*.*
	rmdir $pkgdir/usr/share/mdm/applications

}
