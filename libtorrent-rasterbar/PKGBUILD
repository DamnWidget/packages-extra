# $Id: PKGBUILD 257085 2015-12-21 18:35:20Z fyan $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libtorrent-rasterbar
pkgver=1.0.7
pkgrel=3.1
epoch=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('boost-libs' 'geoip' 'python2')
makedepends=('boost')
options=('!emptydirs')
source=(https://github.com/arvidn/libtorrent/releases/download/libtorrent-${pkgver//./_}/$pkgname-$pkgver.tar.gz
        register_ptr_to_python-for-boost.patch::https://github.com/aresch/libtorrent/commit/5330ba5af717b983e456be9f576d66b78d9ca214.patch)
sha1sums=('db272a0c09a1fad721fe88e6e3942b9160bd0c61'
          'aeef8300f91b2842776feac8ca3717f62539ac74')

prepare() {
  cd $pkgname-$pkgver
  # fix build with new boost
  patch -Np1 -i ../register_ptr_to_python-for-boost.patch
}

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 \
  ./configure \
    --prefix=/usr \
    --enable-python-binding \
    --with-libgeoip=system \
    --with-libiconv
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
