# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

pkgbase=syslinux-themes
pkgname=('manjaro-syslinux-theme' 'sonar-syslinux-theme')
pkgver=20161021
pkgrel=1
arch=('any')
url="https://github.com/manjaro/desktop-settings"
license=('GPL')
depends=('syslinux')
makedepends=('git')
provides=('syslinux-theme')
conflicts=('syslinux-theme' 'isolinux')
replaces=('syslinux-theme' 'isolinux')
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
	date +%Y%m%d
}

package_manjaro-syslinux-theme() {
    pkgdesc='Manjaro Linux syslinux theme'

    local _dest=$pkgdir/usr/share/syslinux/theme
    install -d $_dest
    cp -v $srcdir/desktop-settings/shared/syslinux/manjaro/* $_dest
    cp -v $srcdir/desktop-settings/shared/syslinux/{lng,hlp,cfg,kbdmap}/* $_dest
    install -D $srcdir/desktop-settings/shared/isolinux/isolinux.cfg $pkgdir/usr/share/syslinux/isolinux/isolinux.cfg
}

package_sonar-syslinux-theme() {
    pkgdesc='Sonar Linux syslinux theme'

    local _dest=$pkgdir/usr/share/syslinux/theme
    install -d $_dest
    cp -v $srcdir/desktop-settings/shared/syslinux/sonar/* $_dest
    cp -v $srcdir/desktop-settings/shared/syslinux/{lng,hlp,cfg,kbdmap}/* $_dest
    install -D $srcdir/desktop-settings/shared/isolinux/isolinux.cfg $pkgdir/usr/share/syslinux/isolinux/isolinux.cfg
}
