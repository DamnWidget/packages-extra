# Based on the file created for Arch Linux by: Frank Vanderham

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Guinux <guillaume@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

pkgname=linux319-broadcom-wl
_pkgname=broadcom-wl
pkgver=6.30.223.141
pkgrel=1
pkgdesc='Broadcom 802.11 Linux STA wireless driver BCM43142.'
url='https://bbs.archlinux.org/viewtopic.php?id=145884'
arch=('i686' 'x86_64')
license=('custom')
depends=('linux319')
makedepends=("broadcom-wl-dkms>=$pkgver"
             'dkms'
             'linux319' 'linux319-headers')
groups=('linux319-extramodules')
provides=("$_pkgname=$pkgver")
install=install
source=(broadcom-wl-dkms.conf
        linux317.patch
        linux318.patch)
sha256sums=(b97bc588420d1542f73279e71975ccb5d81d75e534e7b5717e01d6e6adf6a283
            54418874daed844036230e466c5da017bccdaf66461496795f229e427be01442
            3cece51a0675e5b32c3902858b980728ed8d864757b9e71856314e2992cd73f7)

prepare() {
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf

  # workaround to patch wl for linux 3.18
  rm -r $srcdir/dkms/broadcom-wl/$pkgver/source
  cp -r /usr/src/broadcom-wl-$pkgver $srcdir/dkms/broadcom-wl/$pkgver/source
  cd dkms/broadcom-wl/$pkgver/source
  patch -Np1 -i $srcdir/linux317.patch
  patch -Np1 -i $srcdir/linux318.patch
}

build() {
  _extraver=extramodules-3.19-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"

  # build host modules
  msg2 'Build module'
  dkms --dkmsframework dkms.conf build "broadcom-wl/$pkgver" -k "$_kernver"
}

package_linux319-broadcom-wl(){
  _extraver=extramodules-3.19-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"
  depends=('linux319')

  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/broadcom-wl/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/install"
  install -D -m 644 "${srcdir}"/broadcom-wl-dkms.conf "${pkgdir}"/etc/modprobe.d/linux319-broadcom-wl.conf
}
