# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

pkgbase=manjaro-iso-profiles
pkgname=('manjaro-iso-profiles' 'manjaro-iso-profiles-community' 'manjaro-iso-profiles-openrc')
tarball=manjaro-tools-iso-profiles
pkgver=0.9.13.1
pkgrel=3
arch=('any')
url="https://github.com/manjaro/manjaro-tools-iso-profiles"
license=('GPL')
groups=('manjaro-tools')
options=('!strip')
_gitsource=9b693a192851f03ea826af4e47c9f89e7fc1d274
source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::${url}/archive/${_gitsource}.tar.gz")
#source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::$url/archive/${pkgver}-2.tar.gz")


prepare() {
	if [ -e ${srcdir}/${tarball}-${_gitsource} ]; then
		mv ${srcdir}/${tarball}-${_gitsource} ${srcdir}/${tarball}-${pkgver}
	fi
	cd ${tarball}-${pkgver}
	# patches here

	# cleanup (file is for 0.9.14)
	rm shared/Packages-Mhwd-drivers
}

package_manjaro-iso-profiles() {
	pkgdesc='Manjaro Linux official iso profiles'
	depends=("manjaro-tools-iso>=${pkgver}")
	conflicts=('manjaroiso-profiles'
		'manjaroiso-openrc-profiles'
		'manjaroiso-community-profiles')

	cd ${tarball}-${pkgver}
	install -d $pkgdir/usr/share/manjaro-tools/iso-profiles
	cp -a * $pkgdir/usr/share/manjaro-tools/iso-profiles

	install -dm 0755 $srcdir/_community
	mv $pkgdir/usr/share/manjaro-tools/iso-profiles/{cinnamon,lxqt,openbox,enlightenment,i3,netbook,gnome,lxde,mate,pantheon,net-minimal,sysmenu,lxqt-minimal} $srcdir/_community

	install -dm 0755 $srcdir/_openrc
	mv $pkgdir/usr/share/manjaro-tools/iso-profiles/{xfce-openrc,net-openrc} $srcdir/_openrc
}

package_manjaro-iso-profiles-community() {
	pkgdesc='Manjaro Linux community iso profiles'
	depends=('manjaro-iso-profiles')

	install -dm 0755 $pkgdir/usr/share/manjaro-tools/iso-profiles

	mv $srcdir/_community/* $pkgdir/usr/share/manjaro-tools/iso-profiles/
}

package_manjaro-iso-profiles-openrc() {
	pkgdesc='Manjaro Linux openrc profiles'
	depends=('manjaro-iso-profiles')

	install -dm 0755 $pkgdir/usr/share/manjaro-tools/iso-profiles

	mv $srcdir/_openrc/* $pkgdir/usr/share/manjaro-tools/iso-profiles/
}
md5sums=('b6298fbdbcb0f8dafb9d830954ddb061')
