# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

pkgbase=manjaro-iso-profiles
pkgname=('manjaro-iso-profiles' 'manjaro-iso-profiles-community' 'manjaro-iso-profiles-openrc')
pkgver=0.9.11
pkgrel=2
arch=('any')
url="https://github.com/manjaro/manjaro-tools-iso-profiles"
license=('GPL')
groups=('manjaro-tools')
makedepends=('git')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz"
        "kde-fix-Packages-KDE.patch::https://github.com/manjaro/manjaro-tools-iso-profiles/commit/30cb0c753f3f167567aefc32ee7f1ae19e7a0f2a.patch")
sha256sums=('6728f007bb97b85909710225b5f8cc9db58aa052d244692b219a013e1d666dbb'
            'caf5bf4ee66b6bdd4a14b8192d86364770b3e0a19aa3e984cafb454f6a25b737')

prepare() {
	cd *iso-profiles-${pkgver}
	# patches here
	patch -Np1 -i "${srcdir}/kde-fix-Packages-KDE.patch"
}

package_manjaro-iso-profiles() {
	pkgdesc='Manjaro Linux official iso profiles'
	depends=('manjaro-tools-iso>=0.9.8')
	conflicts=('manjaroiso-profiles'
		'manjaroiso-openrc-profiles'
		'manjaroiso-community-profiles')

	cd *iso-profiles-${pkgver}
	install -d $pkgdir/usr/share/manjaro-tools/iso-profiles
	cp -a * $pkgdir/usr/share/manjaro-tools/iso-profiles

	install -dm 0755 $srcdir/_community
	mv $pkgdir/usr/share/manjaro-tools/iso-profiles/{cinnamon,lxqt,openbox,enlightenment,i3,netbook,gnome,lxde,mate,pantheon,net-minimal,sysmenu,lxqt-minimal} $srcdir/_community

	install -dm 0755 $srcdir/_openrc
	mv $pkgdir/usr/share/manjaro-tools/iso-profiles/{xfce-openrc,net-openrc} $srcdir/_openrc
}

package_manjaro-iso-profiles-community() {
	pkgdesc='Manjaro Linux community iso profiles'
	depends=('manjaro-iso-profiles')

	install -dm 0755 $pkgdir/usr/share/manjaro-tools/iso-profiles

	mv $srcdir/_community/* $pkgdir/usr/share/manjaro-tools/iso-profiles/
}

package_manjaro-iso-profiles-openrc() {
	pkgdesc='Manjaro Linux openrc profiles'
	depends=('manjaro-iso-profiles')

	install -dm 0755 $pkgdir/usr/share/manjaro-tools/iso-profiles

	mv $srcdir/_openrc/* $pkgdir/usr/share/manjaro-tools/iso-profiles/
}
