# $Id: PKGBUILD 198567 2013-10-30 15:39:23Z allan $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-session
pkgver=4.11.0
pkgrel=2
_git=e753ff1e14739d7df67ad34647c8247cef7d9922
pkgdesc="A session manager for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('exo' 'libxfce4ui' 'libwnck' 'libsm' 'xorg-iceauth' 'upower' 'xorg-xinit'
         'xorg-xrdb' 'hicolor-icon-theme')
makedepends=('intltool' 'xfce4-dev-tools')
optdepends=('xscreensaver: for locking screen with xflock4'
            'gnome-screensaver: for locking screen with xflock4'
            'xlockmore: for locking screen with xflock4')
replaces=('xfce-utils')
install=$pkgname.install
#source=(http://archive.xfce.org/src/xfce/$pkgname/4.11/$pkgname-$pkgver.tar.bz2
#        xfce4-session-4.10.1-logind-support-for-suspend-hibernate.patch)
source=("$pkgname-$pkgver-$pkgrel.tar.bz2::http://git.xfce.org/xfce/$pkgname/snapshot/$pkgname-$_git.tar.bz2")
sha256sums=('edba2c3aa27b5b788038d79b9374a7b564fd36d3daf77682e692ff908abd913c')

prepare() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
  cd "${srcdir}/${pkgname}-${_git}"

  # Use logind interfaces for suspend and hibernate
  # https://bugzilla.xfce.org/show_bug.cgi?id=9952
  #patch -Np1 -i "$srcdir/xfce4-session-4.10.1-logind-support-for-suspend-hibernate.patch"

  xdt-autogen
}

build() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
  cd "${srcdir}/${pkgname}-${_git}"

  #./configure \
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
  cd "${srcdir}/${pkgname}-${_git}"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
