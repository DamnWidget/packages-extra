# Based on the file created for Arch Linux by:
# Maintainer : Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>

# Maintainer : Philip MÃ¼ller <philm@manjaro.org>
# Maintainer : Rob McCathie <rob@manjaro.org>

pkgname=xfce4-power-manager-dev
_pkgname=xfce4-power-manager
pkgver=1.6.0
pkgrel=1
pkgdesc="Power manager for Xfce desktop (GTK3 version)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'upower' 'libnotify' 'hicolor-icon-theme' 'gtk3')
optdepends=('xfce4-panel: for the Xfce panel plugin'
            'lxpanel: for the LXDE panel plugin')
makedepends=('intltool' 'xfce4-panel' 'lxpanel')
provides=('xfce4-power-manager')
conflicts=('xfce4-power-manager')
source=(http://archive.xfce.org/src/apps/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.bz2)
sha256sums=('bde3157e06ab31ff23648d163d53ee5095f7819544a9f48d3a96d57829857f2b')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-network-manager \
    --enable-polkit \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
