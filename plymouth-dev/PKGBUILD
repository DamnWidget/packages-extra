# Based on the file created for Arch Linux (AUR) by:
# Patrick Burroughs (Celti) <celti@celti.name>
# Abbradar, Zephyr, Christian Autermann, Biginoz, Martin Lee, Ricardo Funke,
# PirateJonno, lh, Cilyan Olowen, Shaffer, Brcha, Lyle Putnam, Det, Boohbah,
# Lara Maia, Padfoot, Jorge Barroso, carstene1ns

# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgname=plymouth-dev
pkgver=0.9.2.r44.g375a65f
pkgrel=1
pkgdesc="A graphical boot splash screen with kernel mode-setting support (development version)"
url="http://www.freedesktop.org/wiki/Software/Plymouth/"

arch=('i686' 'x86_64')
license=('GPL')

depends=('libdrm' 'pango' 'systemd')
makedepends=('git' 'docbook-xsl')
optdepends=('ttf-dejavu')

options=('!libtool' '!emptydirs')

provides=('plymouth')
conflicts=('plymouth')
backup=('etc/plymouth/plymouthd.conf')

source=('git+http://anongit.freedesktop.org/git/plymouth'
        'manjaro-logo.png'
        'plymouth.encrypt_hook'
        'plymouth.encrypt_install'
        'gdm-plymouth.service'
        'kdm-plymouth.service'
        'lxdm-plymouth.service'
        'lightdm-plymouth.service'
        'slim-plymouth.service'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'sd-plymouth.initcpio_install'
        'plymouth-quit.service.in.patch'
        'plymouth-update-initrd.patch')

md5sums=('SKIP'
         'aa243a645a1afb6d438da5bb8e17e0bb'
         'fbdd5d04e3089a214b9056357434df86'
         '65eb269910f1b81e3f0970af456394ca'
         '043410ebf89488602d4e5457f147e48b'
         'dcacff05835053242f52b9e375248ec3'
         '22130f7e6b20d2a43dbed924de75180b'
         '870ea3e63c6989e2badf79d1fbafa914'
         'fb2eb948e2198cfb54cc2b710a05ee61'
         '676b951438542a09929f4c595e1ca3f5'
         '6ebbfee5c8fd1fc8fbf6327b98302b0d'
         '08c426bf7aaaf5da6add6705d022c471'
         'd6fad8d8ef291ea3d85e8e95487c22fa'
         'e017d3d93cba4414198c9c37250993b1')

pkgver() {
  cd plymouth
  git describe --long | sed 's/-/.r/;s/-/./'
}

prepare() {
	cd plymouth

	patch -p1 -i "$srcdir"/plymouth-update-initrd.patch
	patch -p1 -i "$srcdir"/plymouth-quit.service.in.patch
}

build() {
	cd ${pkgname%-*}

	LDFLAGS="$LDFLAGS -ludev" ./autogen.sh \
		--prefix=/usr \
		--exec-prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--sbindir=/usr/bin \
		--enable-systemd-integration \
		--enable-drm \
		--enable-tracing \
		--enable-pango \
		--enable-gtk=no \
		--with-release-file=/etc/os-release \
		--with-logo=/usr/share/plymouth/manjaro-logo.png \
		--with-background-color=0x000000 \
		--with-background-start-color-stop=0x000000 \
		--with-background-end-color-stop=0x4D4D4D \
		--without-rhgb-compat-link \
		--without-system-root-install

	make
}

package() {
	cd plymouth

	make DESTDIR="$pkgdir" install

	install -Dm644 "$srcdir"/manjaro-logo.png "$pkgdir"/usr/share/plymouth/manjaro-logo.png

	install -Dm644 "$srcdir"/plymouth.encrypt_hook "$pkgdir"/usr/lib/initcpio/hooks/plymouth-encrypt
	install -Dm644 "$srcdir"/plymouth.initcpio_hook "$pkgdir"/usr/lib/initcpio/hooks/plymouth
	install -Dm644 "$srcdir"/plymouth.encrypt_install "$pkgdir"/usr/lib/initcpio/install/plymouth-encrypt
	install -Dm644 "$srcdir"/plymouth.initcpio_install "$pkgdir"/usr/lib/initcpio/install/plymouth
	install -Dm644 "$srcdir"/sd-plymouth.initcpio_install "$pkgdir"/usr/lib/initcpio/install/sd-plymouth

	install -dm755 "$pkgdir"/usr/lib/systemd/system
	install -m644 "$srcdir"/{gdm,kdm,lxdm,slim,lightdm}-plymouth.service "$pkgdir"/usr/lib/systemd/system

	ln -s '../systemd-ask-password-plymouth.path' "$pkgdir"/usr/lib/systemd/system/sysinit.target.wants/systemd-ask-password-plymouth.path

	install -Dm644 "$pkgdir"/usr/share/plymouth/plymouthd.defaults "$pkgdir"/etc/plymouth/plymouthd.conf
}
