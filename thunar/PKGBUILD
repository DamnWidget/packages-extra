# $Id: PKGBUILD 198457 2013-10-30 14:59:25Z allan $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=1.6.3
pkgrel=3
_git=defa44cb0b7d1b585b4aa8f308653baac88bd6f1
pkgdesc="Modern file manager for Xfce"
arch=('i686' 'x86_64')
url="http://thunar.xfce.org"
license=('GPL2' 'LGPL2.1')
groups=('xfce4')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify'
         'udev' 'gtk2' 'exo' 'libxfce4util' 'libxfce4ui' 'libpng')
#makedepends=('intltool' 'gtk-doc' 'xfce4-panel')
makedepends=('intltool' 'gtk-doc' 'xfce4-panel' 'xfce4-dev-tools')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'polkit-gnome: for mounting internal partitions (needs root password)'
            'xfce4-panel: for trash applet'
            'tumbler: for thumbnail previews'
            'thunar-volman: manages removable devices'
            'thunar-archive-plugin: create and deflate archives'
            'thunar-media-tags-plugin: view/edit id3/ogg tags')
options=('!libtool')
install=$pkgname.install
#source=(http://archive.xfce.org/src/xfce/$pkgname/1.6/Thunar-$pkgver.tar.bz2)
source=("$pkgname-$pkgver-$pkgrel.tar.bz2::http://git.xfce.org/xfce/$pkgname/snapshot/$pkgname-$_git.tar.bz2")
sha256sums=('b2dc0162fcfd5038c387b1dcda8747182255defa75dff15da1c35356d431af82')

prepare() {
  #cd "${srcdir}/${pkgname}-${pkgver}"
  cd "${srcdir}/${pkgname}-${_git}"
  sed -i -e 's|[git])|[])|g' configure.ac.in
  #patch -Np1 -i "${srcdir}/file.patch"
}

build() {
  #cd "$srcdir/Thunar-$pkgver"
  cd "${srcdir}/${pkgname}-${_git}"
  
  #./configure \
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-dbus \
    --enable-startup-notification \
    --enable-gudev \
    --enable-notifications \
    --enable-exif \
    --enable-pcre \
    --enable-gtk-doc \
    --disable-debug
  make
}

package() {
  #cd "$srcdir/Thunar-$pkgver"
  cd "${srcdir}/${pkgname}-${_git}"

  make DESTDIR=${pkgdir} install
  sed -i 's:x-directory/gnome-default-handler;::' \
    "$pkgdir/usr/share/applications/Thunar-folder-handler.desktop"
}

# vim:set ts=2 sw=2 et:
