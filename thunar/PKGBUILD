# $Id: PKGBUILD 241037 2015-06-19 16:29:43Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=1.6.10
pkgrel=2.1
pkgdesc="Modern file manager for Xfce"
arch=('i686' 'x86_64')
url="http://thunar.xfce.org"
license=('GPL2' 'LGPL2.1')
groups=('xfce4')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify'
         'libgudev' 'gtk2' 'exo' 'libxfce4util' 'libxfce4ui' 'libpng')
makedepends=('intltool' 'xfce4-panel')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'xfce4-panel: for trash applet'
            'tumbler: for thumbnail previews'
            'thunar-volman: manages removable devices'
            'thunar-archive-plugin: create and deflate archives'
            'thunar-media-tags-plugin: view/edit id3/ogg tags')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/Thunar-$pkgver.tar.bz2
        Fix_crashes_when_reloading_target_file_after_move.patch::http://git.xfce.org/xfce/thunar/patch/?id=029012f4c39d9d3d9ae617491a69f76f54a4192f)
sha256sums=('7e9d24067268900e5e44d3325e60a1a2b2f8f556ec238ec12574fbea15fdee8a'
            '292e593492678b7e476d5da992bc9c008169b7e0f00048aee100195701842892')

prepare() {
  cd "$srcdir/Thunar-$pkgver"
  patch -Np1 -i "$srcdir/Fix_crashes_when_reloading_target_file_after_move.patch"
}

build() {
  cd "$srcdir/Thunar-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gio-unix \
    --enable-dbus \
    --enable-gudev \
    --enable-notifications \
    --enable-exif \
    --enable-pcre \
    --disable-debug
  make
}

package() {
  cd "$srcdir/Thunar-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
