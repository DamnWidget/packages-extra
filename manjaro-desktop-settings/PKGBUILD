# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

pkgbase=desktop-settings
pkgname=('manjaro-base-skel'
	'manjaro-xfce-minimal-settings'
	'manjaro-xfce-settings'
	'manjaro-openbox-minimal-settings'
	'manjaro-lxqt-minimal-settings')
pkgver=20160304
pkgrel=1
arch=('any')
url="https://github.com/manjaro/desktop-settings"
license=('GPL')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')
install=$pkgbase.install

_inst_dir(){
	if [[ -d $srcdir/$pkgbase/$1/skel ]];then
		install -d $pkgdir/etc
		cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
	fi

	if [ -d $srcdir/$pkgbase/$1/bash ]; then
		cp -r $srcdir/$pkgbase/$1/bash $pkgdir/etc
	fi

	if [[ -d $srcdir/$pkgbase/$1/scripts ]]; then
		install -d $pkgdir/usr/bin
		cp $srcdir/$pkgbase/$1/scripts/* $pkgdir/usr/bin
	fi

	if [[ -d $srcdir/$pkgbase/$1/schemas ]]; then
		install -d $pkgdir/usr/share/glib-2.0/schemas
		cp $srcdir/$pkgbase/$1/schemas/* $pkgdir/usr/share/glib-2.0/schemas
	fi
}

pkgver() {
	date +%Y%m%d
}

package_manjaro-base-skel() {
	pkgdesc='Manjaro Linux base skeleton files'

	_inst_dir 'shared'
}

package_manjaro-xfce-settings() {
	pkgdesc='Manjaro Linux xfce settings'
	depends=('manjaro-base-skel')
	provides=('manjaro-desktop-settings')

	_inst_dir 'official/xfce'
}

##############minimal######################

package_manjaro-xfce-minimal-settings() {
	pkgdesc='Manjaro Linux xfce-minimal settings'
	depends=('manjaro-base-skel')
	provides=('manjaro-desktop-settings')

	_inst_dir 'minimal/xfce'
}

package_manjaro-openbox-minimal-settings() {
	pkgdesc='Manjaro Linux openbox-minimal settings'
	depends=('manjaro-base-skel')
# 	depends=('manjaro-base-skel' 'obmenu-generator' 'tint2' 'feh' 'openbox-themes')

	_inst_dir 'minimal/openbox'
}

package_manjaro-lxqt-minimal-settings() {
	pkgdesc='Manjaro Linux lxqt-minimal settings'
# 	depends=('manjaro-openbox-minimal-settings' 'compton' 'qterminal'
# 		'pcmanfm-qt' 'lxqt-panel')
	depends=('manjaro-openbox-minimal-settings')
	provides=('manjaro-desktop-settings')

	_inst_dir 'minimal/lxqt'
}

