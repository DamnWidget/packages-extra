# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

_is_live=false
_is_ff=false

_profiles=('xfce-minimal'
	'kde-minimal'
	'xfce'
	'kde'
	'lxqt-minimal'
	'bspwm'
	'budgie'
	'cinnamon'
	'deepin'
	'gnome'
	'i3'
	'lxde'
	'lxqt'
	'mate'
	'netbook'
	'pantheon')

pkgbase=desktop-settings
pkgname=('manjaro-base-skel'
	'manjaro-xfce-minimal-settings'
	'manjaro-kde-minimal-settings'
	'manjaro-xfce-settings'
	'manjaro-kde-settings'
	'manjaro-openbox-minimal-settings'
	'manjaro-lxqt-minimal-settings')
${_is_live} && pkgname+=('manjaro-live-skel')
${_is_ff} && pkgname+=('manjaro-firefox-settings')
pkgver=20160212
pkgrel=2
arch=('any')
url="https://github.com/manjaro/desktop-settings"
license=('GPL')
depends=('manjaro-base-skel')
makedepends=('git')
source=("git+$url.git")
sha256sums=('SKIP')
install=$pkgbase.install

_inst_dir(){
	if [[ -d $srcdir/$pkgbase/$1/skel ]];then
		install -d $pkgdir/etc
		cp -r $srcdir/$pkgbase/$1/skel $pkgdir/etc
	fi

	if [ -d $srcdir/$pkgbase/$1/bash ]; then
		cp -r $srcdir/$pkgbase/$1/bash $pkgdir/etc
	fi

	if [[ -d $srcdir/$pkgbase/$1/scripts ]]; then
		install -d $pkgdir/usr/bin
		cp $srcdir/$pkgbase/$1/scripts/* $pkgdir/usr/bin
	fi

	if [[ -d $srcdir/$pkgbase/$1/schemas ]]; then
		install -d $pkgdir/usr/share/glib-2.0/schemas
		cp $srcdir/$pkgbase/$1/schemas/* $pkgdir/usr/share/glib-2.0/schemas
	fi
}

pkgver() {
	date +%Y%m%d
}

_get_conflicts(){
	local c=()
	for p in ${_profiles[@]};do
		if [[ "$pkgname" != "manjaro-$p-settings" ]];then
			c+=("manjaro-$p-settings")
		fi
	done
	echo ${c[@]}
}

package_manjaro-base-skel() {
	pkgdesc='Manjaro Linux base skeleton files'

	_inst_dir 'shared'
}

package_manjaro-xfce-settings() {
	pkgdesc='Manjaro Linux xfce settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'official/xfce'
}

package_manjaro-kde-settings() {
	pkgdesc='Manjaro Linux kde settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'official/kde'
}

##############minimal######################

package_manjaro-xfce-minimal-settings() {
	pkgdesc='Manjaro Linux xfce-minimal settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'minimal/xfce'
}

package_manjaro-kde-minimal-settings() {
	pkgdesc='Manjaro Linux kde-minimal settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'minimal/kde'
}

package_manjaro-openbox-minimal-settings() {
	pkgdesc='Manjaro Linux openbox-minimal settings'
	conflicts=( $(_get_conflicts) )

	_inst_dir 'minimal/openbox'
}

package_manjaro-lxqt-minimal-settings() {
	pkgdesc='Manjaro Linux lxqt-minimal settings'
	depends=('manjaro-base-skel' 'manjaro-openbox-minimal-settings')
	conflicts=( $(_get_conflicts) )

	_inst_dir 'minimal/lxqt'
}

##############misc######################

package_manjaro-live-skel(){
	pkgdesc='Manjaro Linux live skeleton files'

	_inst_dir 'shared/live'
}

package_manjaro-firefox-settings() {
	pkgdesc='Manjaro Linux firefox settings'
	#depends=('firefox')

	_inst_dir 'shared/firefox'
}
