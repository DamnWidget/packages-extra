# Maintainer: Maxime Gauduin <alucrydarchlinux.org>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=lightdm-gtk-greeter
pkgver=2.0.2
pkgrel=1
epoch=1
pkgdesc='GTK+ greeter for LightDM'
arch=('i686' 'x86_64')
url='https://launchpad.net/lightdm-gtk-greeter'
license=('GPL3' 'LGPL3')
depends=('gtk3' 'lightdm')
makedepends=('exo' 'gobject-introspection' 'intltool')
replaces=('lightdm-gtk2-greeter' 'lightdm-gtk3-greeter')
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
source=("https://launchpad.net/lightdm-gtk-greeter/${pkgver%.?}/${pkgver}/+download/lightdm-gtk-greeter-${pkgver}.tar.gz"
        "a11y.patch"
	'cursor_theme.patch')
sha256sums=('8ee6d93d1d6837b3590f64ac4d5bac5db888a8861dff1cb2ef10f7816ad36690'
            '1c07a8175784f80fbf8e0217f46405ec0726db4e860e8970c647136c5680fb05'
            '58f44a4c71269fd844ef946f47b972096e8a10511c1907fad3171ad8784a8359')

prepare() {
  cd lightdm-gtk-greeter-${pkgver}
  
  # https://bugs.launchpad.net/lightdm-gtk-greeter/+bug/1483864
  patch -Np1 -i ../a11y.patch
  patch -Np1 -i ../cursor_theme.patch
}

build() {
  cd lightdm-gtk-greeter-${pkgver}

  ./configure \
    --prefix='/usr' \
    --libexecdir='/usr/lib/lightdm' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --with-libxklavier \
    --enable-kill-on-sigterm \
    --disable-libido \
    --disable-libindicator \
    --disable-static
  make
}

package() {
  cd lightdm-gtk-greeter-${pkgver}

  make DESTDIR="${pkgdir}" install
}
