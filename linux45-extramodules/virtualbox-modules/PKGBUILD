# Based on the file created for Arch Linux by:
# Ionut Biru <ibiru@archlinux.org>
# Sébastien Luttringer <seblu@aur.archlinux.org>

# Maintainer: Philip Müller <philm@manjaro.org>
# Maintainer: Guinux <guillaume@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>


pkgbase=linux45-virtualbox-modules
pkgname=('linux45-VIRTUALBOX-HOST-MODULES' 'linux45-VIRTUALBOX-GUEST-MODULES')
pkgver=5.0.18
pkgrel=1
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=("virtualbox-host-dkms>=$pkgver"
             "virtualbox-guest-dkms>=$pkgver"
             'virtualbox-dkms-manjaro'
             'dkms'
             'linux45' 'linux45-headers')

_extraver=extramodules-4.5-MANJARO
_pkgver="${pkgver}_OSE"

prepare() {
  _kernver="$(cat /usr/lib/modules/$_extraver/version)"
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
  if [ -e dkms/vboxguest/$pkgver/$_kernver/$CARCH/module/vboxguest.ko ]; then
     rm -v dkms/vboxguest/$pkgver/$_kernver/$CARCH/module/*.ko
     rm -v dkms/vboxhost/$pkgver/$_kernver/$CARCH/module/*.ko
  fi
}

build() {
  _kernver="$(cat /usr/lib/modules/$_extraver/version)"
  # build host modules
  msg2 'Host modules'
  dkms --dkmsframework dkms.conf build "vboxhost/$_pkgver" -k "$_kernver"
  # build guest modules  
  msg2 'Guest modules'
  dkms --dkmsframework dkms.conf build "vboxguest/$_pkgver" -k "$_kernver"
}

package_linux45-VIRTUALBOX-HOST-MODULES(){
  pkgdesc='Host kernel modules for VirtualBox'
  depends=('linux45')
  replaces=('linux45-virtualbox-modules')
  conflicts=('linux45-virtualbox-modules')
  provides=('VIRTUALBOX-HOST-MODULES')
  install=VIRTUALBOX-HOST-MODULES.install
  groups=('linux45-extramodules')

  _kernver="$(cat /usr/lib/modules/$_extraver/version)"

  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/vboxhost/$_pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/VIRTUALBOX-HOST-MODULES.install"

  mkdir -p "$pkgdir/etc/modules-load.d"
  echo "vboxdrv" > "$pkgdir/etc/modules-load.d/linux45-VIRTUALBOX-HOST-MODULES.conf"
  echo "vboxnetadp" >> "$pkgdir/etc/modules-load.d/linux45-VIRTUALBOX-HOST-MODULES.conf"
  echo "vboxnetflt" >> "$pkgdir/etc/modules-load.d/linux45-VIRTUALBOX-HOST-MODULES.conf"
  echo "vboxpci" >> "$pkgdir/etc/modules-load.d/linux45-VIRTUALBOX-HOST-MODULES.conf"
}

package_linux45-VIRTUALBOX-GUEST-MODULES(){
  pkgdesc='Guest kernel modules for VirtualBox'
  license=('GPL')
  depends=('linux45')
  replaces=('linux45-virtualbox-manjaro-modules')
  conflicts=('linux45-virtualbox-manjaro-modules')
  provides=('VIRTUALBOX-GUEST-MODULES')
  install=VIRTUALBOX-GUEST-MODULES.install
  groups=('linux45-extramodules')

  _kernver="$(cat /usr/lib/modules/$_extraver/version)"

  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/vboxguest/$_pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/VIRTUALBOX-GUEST-MODULES.install"

  mkdir -p "$pkgdir/etc/modules-load.d"
  echo "vboxguest" > "$pkgdir/etc/modules-load.d/linux45-VIRTUALBOX-GUEST-MODULES.conf"
  echo "vboxsf" >> "$pkgdir/etc/modules-load.d/linux45-VIRTUALBOX-GUEST-MODULES.conf"
  echo "vboxvideo" >> "$pkgdir/etc/modules-load.d/linux45-VIRTUALBOX-GUEST-MODULES.conf"
}

# vim:set ts=2 sw=2 et:
