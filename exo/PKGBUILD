# $Id: PKGBUILD 232394 2015-03-01 19:43:45Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=0.10.3
pkgrel=2.1
pkgdesc="Extensions to Xfce by os-cillation"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('libxfce4ui' 'gtk2' 'hicolor-icon-theme' )
makedepends=('perl-uri' 'intltool' 'gtk-doc' 'xorg-iceauth')
optdepends=('perl-uri: for mail-compose helper script')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        0001-Don-t-quote-arguments-passed-to-terminal-emulators-b.patch::http://git.xfce.org/xfce/exo/patch/?id=b4200c9bab9f60e61a100dd12d841c2588b360d8
        0002-Fix-StartupNotify.patch::http://git.xfce.org/xfce/exo/patch/?id=3a2c05bd68f924f5c8bd530a2fe8f4cc5b671b16)
sha256sums=('95704b14ef9065268c3aa9a7c0bacbe0ddb490a183a4f5ab13bba4a1722826bd'
            'ddb7e656408ed65537316eaa4eaf7fdef7062a0a40750155dfa6e82bdde7689a'
            '85ddab00a23f738f923a64435a77c0c8214c69a85d4163af44e9d19326d701b1')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=10731#c4
  patch -Np1 -i ../0001-Don-t-quote-arguments-passed-to-terminal-emulators-b.patch
  # https://bugzilla.xfce.org/show_bug.cgi?id=9570#c15
  patch -Np1 -i ../0002-Fix-StartupNotify.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var  \
    --disable-static \
    --enable-gtk-doc \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
