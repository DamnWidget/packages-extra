# $Id: PKGBUILD 216752 2014-07-09 19:10:40Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

## Manjaro temporary maintainer: Rob McCathie <rob@manjaro.org>
## Added left-click-dirty-workaround.patch

pkgbase=network-manager-applet
pkgname=(network-manager-applet libnm-gtk)
pkgver=0.9.10.0
pkgrel=2.1
pkgdesc="Applet for managing network connections"
arch=(i686 x86_64)
license=(GPL2 LGPL2.1)
url="http://www.gnome.org/projects/NetworkManager/"
makedepends=(libnm-glib libsecret gtk3 gnome-icon-theme mobile-broadband-provider-info iso-codes
             libnotify libmm-glib intltool gobject-introspection python2)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.9/$pkgname-$pkgver.tar.xz
        "left-click-dirty-workaround.patch::https://bugzilla.redhat.com/attachment.cgi?id=951196&action=diff&context=patch&collapsed=&headers=1&format=raw")
sha256sums=('f5c26c692f538dc0145dc7d46d3d390754fad73d0ff06861c074c61f3dc54eca'
            'dd93f067bb2cade79f7eec6a87c7f706cbb735b6bd65981acc19ba9d27b9d6a2')

prepare() {
  cd $pkgbase-$pkgver
  
  # https://bugzilla.redhat.com/show_bug.cgi?id=1149335
  # https://bbs.archlinux.org/viewtopic.php?pid=1467359
  patch -p0 -i "${srcdir}/left-click-dirty-workaround.patch"
}

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/networkmanager \
      --disable-static \
      --disable-maintainer-mode \
      --disable-migration \
      --with-modem-manager-1

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgbase-$pkgver
  make -k check
}

package_network-manager-applet() {
  depends=(libnm-gtk libmm-glib libnotify libsecret)
  install=network-manager-applet.install

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C src/libnm-gtk uninstall
}

package_libnm-gtk() {
  pkgdesc="NetworkManager GUI library"
  depends=(libnm-glib gtk3 mobile-broadband-provider-info iso-codes)

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" -C src/libnm-gtk install
}
