# Maintainer: Manuel Barrette (manb[at]manjaro[dot]org)
# Contributor: Jonathon Fernyhough (jonathon[at]manjaro[dot]org)

pkgbase=manjaro-documentation
pkgname=('manjaro-documentation')
pkgver=20160828
_gitcommit=5dab7b6e98bd3d95a98c1a462413428c53503b0d
pkgrel=1
arch=('any')
url="https://github.com/manjaro/manjaro-user-guide"
makedepends=(lyx texlive-core texlive-latexextra texlive-pictures ttf-comfortaa ghostscript)
source=("manjaro-documentation-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz")
sha256sums=('ab3ed19288f0a4ce6718936a0d782b82cf42e22fc68b4c5fb510b24124484200')

prepare() {
  cd ${srcdir}/manjaro-user-guide*
  lyx --export pdflatex manjaro-user-guide.lyx
  pdflatex manjaro-user-guide
  texindy --language english manjaro-user-guide.idx
  pdflatex manjaro-user-guide
  pdflatex manjaro-user-guide
  gs -dNOPAUSE -dBATCH -sDEVICE=pdfwrite -dCompatibilityLevel=1.4 -dPDFSETTINGS=/printer -sOutputFile=manjaro-user-guide-printer.pdf manjaro-user-guide.pdf
}

package() {
  pkgdesc="Manjaro Beginner User Guide"
  license=('CC-BY-SA 4.0')
  cd ${srcdir}/manjaro-user-guide*
  install -Dm644 manjaro-user-guide-printer.pdf "${pkgdir}/usr/share/doc/livecd/Beginner_User_Guide.pdf"
}
