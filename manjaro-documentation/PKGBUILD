# Maintainer: Manuel Barrette (manb[at]manjaro[dot]org)
# Contributor: Jonathon Fernyhough (jonathon[at]manjaro[dot]org)

pkgbase=manjaro-documentation
pkgname=('manjaro-documentation')
pkgver=20161024
_gitcommit=f7bb3ad4f2cd6454c39d4e38f641c2f99fb4e330
pkgrel=2
arch=('any')
url="https://github.com/manjaro/manjaro-user-guide"
makedepends=('ghostscript' 'lyx' 'python2' 'texlive-core' 'texlive-latexextra' 'texlive-pictures' 'ttf-comfortaa')
source=("manjaro-documentation-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz"
	'manjaro-documentation'
	'manjaro-documentation.desktop')
sha256sums=('00c61a4e411be029e14463fc40b124145ace678082395644dcdfcef141e5854f'
            'b122d1f817d60b5a18b2bb404d54c2267307b6bb0c215fc897c58cc81226a80e'
            '51b209d9da5bb7d7a6b88abe097702b603c9bcfea340753187221a12955a92dd')

prepare() {
  cd ${srcdir}/manjaro-user-guide-$_gitcommit
  lyx --export pdflatex manjaro-user-guide.lyx
  pdflatex manjaro-user-guide
  texindy --language english manjaro-user-guide.idx
  pdflatex manjaro-user-guide
  pdflatex manjaro-user-guide
  gs -dNOPAUSE -dBATCH -sDEVICE=pdfwrite -dCompatibilityLevel=1.4 -dPDFSETTINGS=/printer \
  	-sOutputFile=manjaro-user-guide-printer.pdf manjaro-user-guide.pdf
}

package() {
  pkgdesc="Manjaro Beginner User Guide"
  license=('CC-BY-SA 4.0')
  install -Dm644 "$srcdir/manjaro-user-guide-$_gitcommit/manjaro-user-guide-printer.pdf" \
  	"$pkgdir/usr/share/doc/livecd/Beginner_User_Guide.pdf"
  install -Dm755 manjaro-documentation "$pkgdir/usr/bin/manjaro-documentation"
  install -Dm644 manjaro-documentation.desktop "$pkgdir/usr/share/applications/manjaro-documentation.desktop"
}
