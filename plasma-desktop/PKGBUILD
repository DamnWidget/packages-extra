# $Id: PKGBUILD 257542 2016-01-06 15:37:45Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=plasma-desktop
pkgver=5.5.4
pkgrel=1.1
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"
        "Test-against-provide-value.patch::https://quickgit.kde.org/?p=plasma-desktop.git&a=commitdiff&h=68c132fa7cf7589ac3b7535699f5128dcda256d1&hp=eb3b3006b9e275688481be7eb57b798f73d12faf&o=plain")
depends=('powerdevil' 'polkit-kde-agent' 'libcanberra' 'libxkbfile' 'kmenuedit'
         'systemsettings' 'ksysguard' 'kpeople' 'baloo' 'qt5-graphicaleffects')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'xf86-input-evdev' 'xf86-input-synaptics' 'xorg-server-devel')
optdepends=('plasma-nm: Network manager applet')
conflicts=('kdebase-workspace' 'kcm-touchpad-frameworks')
replaces=('kcm-touchpad-frameworks')
install="${pkgname}.install"
groups=('plasma')
md5sums=('2ec4ddd309582c91c39f1921415cc059'
         'c295faca49e0cc4a9181f4535951a973')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i $srcdir/Test-against-provide-value.patch
  cd ..
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
