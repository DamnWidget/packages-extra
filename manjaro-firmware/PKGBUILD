#
# Maintainer: Phil Miller <philm@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>


pkgname=('manjaro-firmware')
pkgver=20$(date +%y%m%d)
pkgrel=1
_b43=5.100.138
_legacy=3.130.20.0
_nouveau=325.15
arch=('any')
url="http://manjarolinux.org"
license=('GPL' 'MPL' 'custom')
makedepends=('b43-fwcutter' 'python2')
pkgdesc="Extra firmwares for Manjaro Linux"
provides=('nouveau-fw' 'b43-firmware-classic' 'b43-firmware-legacy')
conflicts=('nouveau-fw' 'b43-firmware-classic' 'b43-firmware' 'b43-firmware-legacy')

source=("http://www.lwfinger.com/b43-firmware/broadcom-wl-${_b43}.tar.bz2"
        "http://downloads.openwrt.org/sources/wl_apsta-${_legacy}.o"
        "https://raw.github.com/imirkin/re-vp2/master/extract_firmware.py"
        "http://us.download.nvidia.com/XFree86/Linux-x86/${_nouveau}/NVIDIA-Linux-x86-${_nouveau}.run")
sha256sums=('f1e7067aac5b62b67b8b6e4c517990277804339ac16065eb13c731ff909ae46f'
            '7dba610b1d96dd14e901bcbce14cd6ecd1b1ac6f5c0035b0d6b6dc46a7c3ef90'
            'SKIP'
            '3d790e4bfed24641f7cc76879144ab5d52b12271012ba381b0d33aa1a2e08775')

build() {
  sh NVIDIA-Linux-x86-${_nouveau}.run --extract-only
  python2 extract_firmware.py
}

package() {
  install -d ${pkgdir}/usr/lib/firmware/

  # install b43-firmware
  b43-fwcutter -w "${pkgdir}/usr/lib/firmware/" ${srcdir}/broadcom-wl-${_b43}/linux/wl_apsta.o

  # install b43-legacy-firmware
  b43-fwcutter -w "${pkgdir}/usr/lib/firmware/" ${srcdir}/wl_apsta-${_legacy}.o

  # install nouveau firmwares
  install -dm 0755 "${pkgdir}"/usr/lib/firmware/nouveau/
  cp -a nv* vuc-* "${pkgdir}"/usr/lib/firmware/nouveau/
}
