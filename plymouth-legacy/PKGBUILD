# Based on the file created for Arch Linux by:
# Det <nimetonmaili at gmail dot com>
# PirateJonno <j@skurvy.no-ip.org>
# Biginoz <biginoz _at_ free _point_ fr>
# M0Rf30 <morfeo89@hotmail.it>

# Maintainer: Guinux <nuxgui@gmail.com>

pkgname=plymouth-legacy
_pkgname=plymouth
pkgver=0.8.8
pkgrel=3
pkgdesc="A graphical boot splash screen with kernel mode-setting support"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
depends=('libdrm' 'pango' 'systemd' 'gtk2')
makedepends=('docbook-xsl')
optdepends=('ttf-dejavu')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
provides=('plymouth')
conflicts=('plymouth' 'plymouth-dev')
backup=('etc/plymouth/plymouthd.conf')
source=("http://www.freedesktop.org/software/$_pkgname/releases/$_pkgname-$pkgver.tar.bz2"
        'manjaro-logo.png'
        "plymouth.encrypt_hook"
        "plymouth.encrypt_install"
        'gdm-plymouth.service'
        'kdm-plymouth.service'
        'kdm-unpatched-plymouth.service'
        'lightdm-plymouth.service'
        'lxdm-plymouth.service'
        'slim-plymouth.service'
        'sddm-plymouth.service'
        'plymouth.functions'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-pid.patch'
        'plymouth-quit.service'
        'plymouth-quit.service.in.patch'
        'plymouth-quit-wait.service'
        'plymouth-set-default-theme.in.patch'
        'plymouth-update-initrd.patch'
        'plymouthd.conf'
        'systemd-unit-dir.patch'
        'udevadm.patch')
md5sums=('38f5e613e5ab17806b950cee2d0d0d4e'
         'aa243a645a1afb6d438da5bb8e17e0bb'
         'fbdd5d04e3089a214b9056357434df86'
         '65eb269910f1b81e3f0970af456394ca'
         '043410ebf89488602d4e5457f147e48b'
         'dcacff05835053242f52b9e375248ec3'
         '4dfcd9bb81195c6589defc655363a433'
         '870ea3e63c6989e2badf79d1fbafa914'
         '22130f7e6b20d2a43dbed924de75180b'
         'fb2eb948e2198cfb54cc2b710a05ee61'
         '9b18cf852fe883e7074104627f991d89'
         'f0110fba1e77cd22c32d1727399d0b8a'
         '4f3be343eea9956bbc94d615d5f41def'
         'e4bd87da9993aefb1e56e8072195e892'
         'e4d804825708c4ae555acd6489925b35'
         'e624903dcd6b795184340b3bbda437d8'
         '9d51efe40a5a75426c8faeb905e46600'
         '80892a86b3d2b2642627d4c255ff16a8'
         '42fddd683720dbacbe6e30b4b7892f3c'
         'c2a0cf5a499d6c14723461166a24ede7'
         '37479b134827dc307b3c55bfb2711e99'
         '30acbfc7722fb62ee464b8fec32b5cfd'
         '89014b04513d61e0085e7a0c9924c183')

prepare() {
    cd $srcdir 
    msg "Applying Patches..."
    
    cd $srcdir/$_pkgname-$pkgver
    msg2 "Setting a human-readable error for '# plymouth-set-default-theme -R'"
    patch -p0 -i ../plymouth-set-default-theme.in.patch

    msg2 "Fixing updating initrd"
    patch -p0 -i ../plymouth-update-initrd.patch

    msg2 "Changing PID file to /run/plymouth.pid"
    patch -p1 -i ../plymouth-pid.patch

    msg2 "Changing plymout quit service"
    patch -p1 -i ../plymouth-quit.service.in.patch
    
    msg2 "Changing udevadm path"
    patch -p1 -i ../udevadm.patch  

    msg2 "Changing SYSTEMD_UNIT_DIR to /usr/lib/systemd/system/"
    patch -p0 -i ../systemd-unit-dir.patch
    
    sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' \
       -i src/libply-splash-graphics/ply-image.c
}

build() {
    cd $srcdir/$_pkgname-$pkgver
    
    msg "Starting make..."

    ./configure --prefix=/usr \
                --exec-prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --libdir=/usr/lib \
                --libexecdir=/usr/lib \
                --sbindir=/usr/bin \
                --enable-systemd-integration \
                --enable-drm \
                --enable-tracing \
                --enable-pango \
                --without-rhgb-compat-link \
                --with-gdm-autostart-file=yes \
                --enable-gdm-transition \
                --with-gdm-autostart-file=yes \
                --with-release-file=/etc/manjaro-release \
                --with-logo=/usr/share/plymouth/manjaro-logo.png \
                --with-background-start-color-stop=0x000000 \
                --with-background-end-color-stop=0x4D4D4D \
                --without-system-root-install

    make
}

package() {
    cd $srcdir/$_pkgname-$pkgver

    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/plymouth.initcpio_install" "$pkgdir/usr/lib/initcpio/install/plymouth"
    install -Dm644 "$srcdir/plymouth.initcpio_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth"
    install -Dm644 "$srcdir/plymouth.encrypt_install" "$pkgdir/usr/lib/initcpio/install/plymouth-encrypt"
    install -Dm644 "$srcdir/plymouth.encrypt_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth-encrypt"
    install -Dm644 "$srcdir/plymouth.functions" "$pkgdir/etc/rc.d/functions.d/plymouth.functions"
    install -Dm644 "$srcdir/manjaro-logo.png" "$pkgdir/usr/share/plymouth/manjaro-logo.png"
    install -Dm644 "$srcdir/plymouthd.conf" "$pkgdir/etc/plymouth/plymouthd.conf"

    # Plymouth<->systemd integration, including units for DMs
    for i in {{gdm,kdm{,-unpatched},lightdm,lxdm,sddm,slim}-plymouth,plymouth-quit-wait,plymouth-quit}.service; do
        install -Dm644 "$srcdir/$i" "$pkgdir/usr/lib/systemd/system/$i"
    done
}
