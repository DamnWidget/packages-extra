# Based on the file created for Arch Linux by: Frank Vanderham

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Guinux <guillaume@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

# Contributor: Frank Vanderham <twelve.eighty (at) gmail.>
# Contributor: Hsin-Yi Chen (hychen) <hycehn (at) canonical.com>
# Contributor: Alberto Milone <alberto.milone (at) canonical.com>
# Contributor: Jasmine Hassan <jasmine.aura (at) gmail.com>
# Contributor: b3niup

pkgname=broadcom-wl-dkms
_pkgname=broadcom-wl
pkgver=6.30.223.271
pkgrel=1
pkgdesc="Broadcom 802.11 Linux STA wireless driver"
url='http://www.broadcom.com/support/802.11/linux_sta.php'
arch=('i686' 'x86_64')
license=('custom')
depends=('dkms')
optdepends=('linux-headers: If running standard kernel, otherwise find matching headers for your kernel')
install=broadcom-wl-dkms.install
backup=('etc/modprobe.d/broadcom-wl-dkms.conf')
conflicts=('broadcom-wl')
source=('broadcom-wl-dkms.conf'
	'dkms.conf'
        'license.patch'
	'wl_linux.c.patch'
        'linux47.patch'
	'linux48.patch')
sha256sums=('b4aca51ac5ed20cb79057437be7baf3650563b7a9d5efc515f0b9b34fbb9dc32'
            '4e719f9c8468f7c802bb0e52939f2680abb1287d3817d91252958f0ae82f983b'
            '2f70be509aac743bec2cc3a19377be311a60a1c0e4a70ddd63ea89fae5df08ac'
            '583335a6edb2ed1094977c401643d3e60471c6bc5c3ea81f45910a9cccb06032'
            '30ce1d5e8bf78aee487d0f3ac76756e1060777f70ed1a9cf95215c3a52cfbe2e'
            '833af3b209d6a101d9094db16480bda2ad9a85797059b0ae0b13235ad3818e9c')
sha256sums_i686=('4f8b70b293ac8cc5c70e571ad5d1878d0f29d133a46fe7869868d9c19b5058cd')
sha256sums_x86_64=('5f79774d5beec8f7636b59c0fb07a03108eef1e3fd3245638b20858c714144be')
source_x86_64=("https://docs.broadcom.com/docs-and-downloads/docs/linux_sta//hybrid-v35_64-nodebug-pcoem-${pkgver//./_}.tar.gz")
source_i686=("https://docs.broadcom.com/docs-and-downloads/docs/linux_sta//hybrid-v35-nodebug-pcoem-${pkgver//./_}.tar.gz")
backup=('etc/modprobe.d/broadcom-wl-dkms.conf')
build() {
	cd "${srcdir}"

	patch -p1 -i license.patch
	patch -p1 -i wl_linux.c.patch
	patch -p1 -i linux47.patch
	patch -p1 -i linux48.patch

	sed -e "/BRCM_WLAN_IFNAME/s:eth:wlan:" \
		-i src/wl/sys/wl_linux.c

	# delete files not needed for packaging
	rm *.tar.gz
}
package() {
	cd "${srcdir}"
	
	mkdir -p ${pkgdir}/usr/src/${_pkgname}-${pkgver}

	cp -RL * ${pkgdir}/usr/src/${_pkgname}-${pkgver}
	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 broadcom-wl-dkms.conf "${pkgdir}"/etc/modprobe.d/broadcom-wl-dkms.conf
}
