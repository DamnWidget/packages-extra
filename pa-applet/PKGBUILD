# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dot]org>
# Contributor: Nicolas Avrutin  <nicolasavru[at]gmail[dot]com>
# Contributor: Marc MAURICE <dooblem[at]positon[dot]org>

pkgname=pa-applet
pkgver=1.0.2
pkgrel=2
_ver=33b413b83234d457b9512219cf4c1020eb99a3de
pkgdesc="PulseAudio system tray applet with volume bar"
arch=(i686 x86_64)
url="https://github.com/fernandotcl/pa-applet"
license=('BSD')
depends=('gtk3' 'libnotify' 'libcanberra' 'pulseaudio')
options=('!libtool')
replaces=('pa-applet-git')
conflicts=('pa-applet-git')
source=("pa-applet-$pkgver.zip::https://github.com/fernandotcl/pa-applet/archive/$_ver.zip" pa-applet.desktop)
sha256sums=('4ae82998a521278bcb45640d7546cd53ad9d72aa1a4a934b84aa569f5089b8ea'
            '99db3caaf0abb403480b466d6b937c9a72c9fc17e86c07bbeaf9c973ce8da303')

build() {
  cd "${srcdir}/${pkgname}-${_ver}"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_ver}"
  make DESTDIR="$pkgdir/" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/etc/xdg/autostart/${pkgname}.desktop"
}
