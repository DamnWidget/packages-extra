# Maintainer: Philip MÃ¼ller <philm_at_manjaro_dot_org>
# Maintainer: Rob McCathie <rob_at_manjaro_dot_org>

pkgbase=menda-themes
pkgname=("${pkgbase}" "${pkgbase}-dark")
pkgver=20160211
pkgrel=1
_git='60d6f7886b39f5f6d739456cab55d4d083274dac'
arch=('any')
url="https://github.com/manjaro/artwork-menda"
license=('GPL3')
depends=('gtk-engine-murrine' 'gtk-engines')
optdepends=('menda-circle-icon-theme: Recommended icon theme'
            'faenza-green-icon-theme: Recommended icon theme')
source=("${pkgbase}-${pkgver}-${pkgrel}.tar.gz::${url}/archive/${_git}.tar.gz")
md5sums=('a78f49c851c848615cc16872e4a605ec')

package_menda-themes() {
  pkgdesc="Manjaro's official Gtk2, Gtk3, Metacity, Xfwm4, Openbox, Cinnamon and GNOME Shell themes"
  replaces=('gtk-theme-menda')
  
  install -dm755 "${pkgdir}/usr/share/themes"
  install -Dm755 "${srcdir}/artwork-menda-${_git}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cp -r "${srcdir}/artwork-menda-${_git}/Menda" "${pkgdir}/usr/share/themes"
}

package_menda-themes-dark() {
  pkgdesc="Manjaro's official Gtk2, Gtk3, Metacity, Xfwm4, Openbox, Cinnamon and GNOME Shell themes. Dark version."
  
  install -dm755 "${pkgdir}/usr/share/themes"
  install -Dm755 "${srcdir}/artwork-menda-${_git}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cp -r "${srcdir}/artwork-menda-${_git}/Menda-Dark" "${pkgdir}/usr/share/themes"
}
