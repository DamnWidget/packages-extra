# $Id: PKGBUILD 250499 2015-11-09 22:49:43Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=3.18.2+git946ea67
pkgrel=1
_git=946ea67c7680abf5d0f27b71e532516a36594f57
pkgdesc="Screen reader for individuals who are blind or visually impaired"
arch=(any)
license=('LGPL')
url="http://www.gnome.org/projects/orca"
install=orca.install
depends=(gtk3 at-spi2-atk python-atspi python-dbus python-xdg speech-dispatcher liblouis brltty
         xorg-xmodmap hicolor-icon-theme gsettings-desktop-schemas)
makedepends=(itstool intltool python2 yelp-tools)
groups=(gnome-extra)
source=($pkgname-$pkgver.tar.gz::https://github.com/GNOME/orca/archive/$_git.tar.gz)
#source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('06b0493e9a2e684a98fbd42ffc7c0865a8a398355c66ad5687127ec110876610')

prepare() {
  mv $pkgname-$_git $pkgname-$pkgver
  sed -i 's|3pre|2|g' $pkgname-$pkgver/configure.ac
}

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  #./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
