# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

_mt_ver=0.12.2

pkgbase=manjaro-iso-profiles
pkgname=('manjaro-iso-profiles-base'
	'manjaro-iso-profiles-official'
	'manjaro-iso-profiles-community'
	'manjaro-iso-profiles-minimal'
	'sonar-iso-profiles')
pkgver=16.07
pkgrel=1
arch=('any')
url="https://github.com/manjaro/manjaro-tools-iso-profiles"
license=('GPL')
# options=('!strip')
_git=e7139d7fc274ff7b9d8c6d0ac522090c8c202e78
source=(#"${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
        "${pkgbase}-${pkgver}.tar.gz::$url/archive/${_git}.tar.gz")
sha256sums=('405edc984d1a01f6a03c660b41002a923bcc42459ea6df40edca90bcd8ec3f70')

prepare(){
 	# remove this
  	mv $srcdir/manjaro-tools-iso-profiles-$_git $srcdir/manjaro-tools-iso-profiles-$pkgver
}

_inst_dir(){
	install -d $pkgdir/usr/share/manjaro-tools/iso-profiles
	cp -a $srcdir/manjaro-tools-iso-profiles-$pkgver/$1 $pkgdir/usr/share/manjaro-tools/iso-profiles/
}

package_manjaro-iso-profiles-base() {
	pkgdesc='Manjaro Linux iso profiles base'
	provides=('iso-profiles-base')
	conflicts=('manjaro-tools-iso-profiles' 'manjaro-iso-profiles' 'manjaro-iso-profiles-openrc')
	replaces=('manjaro-tools-iso-profiles' 'manjaro-iso-profiles' 'manjaro-iso-profiles-openrc')

	_inst_dir 'shared'
	echo "version_min=$_mt_ver" > $pkgdir/usr/share/manjaro-tools/iso-profiles/.buildiso
}

package_manjaro-iso-profiles-official() {
	pkgdesc='Manjaro Linux official iso profiles'
	groups=('manjaro-tools')
	depends=("manjaro-tools-iso>=${_mt_ver}")

	_inst_dir 'official'
}

package_manjaro-iso-profiles-community() {
	pkgdesc='Manjaro Linux community iso profiles'
	groups=('manjaro-tools')
	depends=("manjaro-tools-iso>=${_mt_ver}")

	_inst_dir 'community'
}

package_manjaro-iso-profiles-minimal() {
	pkgdesc='Manjaro Linux minimal iso profiles'
	groups=('manjaro-tools')
	depends=("manjaro-tools-iso>=${_mt_ver}")
	conflicts=('manjaro-iso-profiles-community-minimal')
	replaces=('manjaro-iso-profiles-community-minimal')

	_inst_dir 'minimal'
}

package_sonar-iso-profiles() {
	pkgdesc='Sonar Linux iso profiles'
	groups=('manjaro-tools')
	depends=("manjaro-tools-iso>=${_mt_ver}")

	_inst_dir 'sonar'
}
