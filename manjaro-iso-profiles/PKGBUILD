# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

_mt_ver=0.10.0

pkgbase=manjaro-iso-profiles
pkgname=('manjaro-iso-profiles-base'
	'manjaro-iso-profiles-official'
	'manjaro-iso-profiles-community'
	'manjaro-iso-profiles-minimal'
	'sonar-iso-profiles')
pkgver=16.01
pkgrel=1
arch=('any')
url="https://github.com/manjaro/manjaro-tools-iso-profiles"
license=('GPL')
# options=('!strip')
#_git=ec649a8dfb6739ef19a7a8c5020be07c453e8dd3
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
       # "${pkgbase}-${pkgver}.tar.gz::$url/archive/${_git}.tar.gz")
sha256sums=('1f47c7d0b7fe9d2e7da35c58fb5a4c309002c14fd1eb33aaa9bce0f35bc694fc')

# prepare(){
# 	# remove this
# 	mv $srcdir/manjaro-tools-iso-profiles-$_git $srcdir/manjaro-tools-iso-profiles-$pkgver
# }

_inst_dir(){
	install -d $pkgdir/usr/share/manjaro-tools/iso-profiles
	cp -a $srcdir/manjaro-tools-iso-profiles-$pkgver/$1 $pkgdir/usr/share/manjaro-tools/iso-profiles/
}

package_manjaro-iso-profiles-base() {
	pkgdesc='Manjaro Linux iso profiles base'
	provides=('iso-profiles-base')
	depends=("manjaro-tools-iso>=${_mt_ver}")
	conflicts=('manjaro-iso-profiles-openrc')
	replaces=('manjaro-iso-profiles-openrc')

	_inst_dir 'shared'
	echo "version_min=$_mt_ver" > $pkgdir/usr/share/manjaro-tools/iso-profiles/.buildiso
}

package_manjaro-iso-profiles-official() {
	pkgdesc='Manjaro Linux official iso profiles'
	groups=('manjaro-tools')
	depends=('manjaro-iso-profiles-base')

	_inst_dir 'official'
}

package_manjaro-iso-profiles-community() {
	pkgdesc='Manjaro Linux community iso profiles'
	groups=('manjaro-tools')
	depends=('manjaro-iso-profiles-base')

	_inst_dir 'community'
}

package_manjaro-iso-profiles-minimal() {
	pkgdesc='Manjaro Linux minimal iso profiles'
	groups=('manjaro-tools')
	depends=('manjaro-iso-profiles-base')
	conflicts=('manjaro-iso-profiles-community-minimal')
	replaces=('manjaro-iso-profiles-community-minimal')

	_inst_dir 'minimal'
}

package_sonar-iso-profiles() {
	pkgdesc='Sonar Linux iso profiles'
	groups=('manjaro-tools')
	depends=('manjaro-iso-profiles-base')

	_inst_dir 'sonar'
}
