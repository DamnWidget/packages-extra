# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Contributor: artoo <artoo@manjaro.org>

_mt_ver=0.14.0
_mt_pkgver=0.14.0

pkgbase=manjaro-iso-profiles
pkgname=('manjaro-iso-profiles-base'
        'manjaro-iso-profiles-official'
        'manjaro-iso-profiles-community'
        #'manjaro-iso-profiles-minimal'
        'sonar-iso-profiles')
pkgver=17.0beta2
pkgrel=2
arch=('any')
url="https://github.com/manjaro/iso-profiles"
license=('GPL')
_git=2d6a0a8ff1d111a584d63914d35a31ad351ee197
source=("$url/archive/${_git}.tar.gz")
sha256sums=('36f66e1f1beb5e0621715bde9286b7e6211057fe5078532e990e0f1d5ac10904')

prepare(){
    # remove this
    mv $srcdir/iso-profiles-$_git $srcdir/iso-profiles-$pkgver
}

_inst_dir(){
    install -d $pkgdir/usr/share/manjaro-tools/iso-profiles
    cp -a $srcdir/iso-profiles-$pkgver/$1 $pkgdir/usr/share/manjaro-tools/iso-profiles/
}

package_manjaro-iso-profiles-base() {
    pkgdesc='Manjaro Linux iso profiles base'
    provides=('iso-profiles-base')
    conflicts=('manjaro-tools-iso-profiles' 'manjaro-iso-profiles' 'manjaro-iso-profiles-openrc')
    replaces=('manjaro-tools-iso-profiles' 'manjaro-iso-profiles' 'manjaro-iso-profiles-openrc')
    optdepends=('manjaro-iso-profiles-official: Manjaro Linux ISO-Image official profiles'
                'manjaro-iso-profiles-community: Manjaro Linux ISO-Image community profiles'
                'manjaro-iso-profiles-minimal: Manjaro Linux ISO-Image minimal profiles')

    _inst_dir 'shared'
    echo "version_min=$_mt_ver" > $pkgdir/usr/share/manjaro-tools/iso-profiles/.buildiso
}

package_manjaro-iso-profiles-official() {
    pkgdesc='Manjaro Linux official iso profiles'
    groups=('manjaro-tools')
    depends=("manjaro-tools-yaml>=${_mt_pkgver}")
    replaces=('manjaro-iso-profiles-minimal')

    _inst_dir 'manjaro'
}

package_manjaro-iso-profiles-community() {
    pkgdesc='Manjaro Linux community iso profiles'
    groups=('manjaro-tools')
    depends=("manjaro-tools-yaml>=${_mt_pkgver}")

    _inst_dir 'community'
}

package_manjaro-iso-profiles-minimal() {
    pkgdesc='Manjaro Linux minimal iso profiles'
    groups=('manjaro-tools')
    depends=("manjaro-tools-yaml>=${_mt_pkgver}")
    conflicts=('manjaro-iso-profiles-community-minimal')
    replaces=('manjaro-iso-profiles-community-minimal')

    _inst_dir 'minimal'
}

package_sonar-iso-profiles() {
    pkgdesc='Sonar Linux iso profiles'
    groups=('manjaro-tools')
    depends=("manjaro-tools-yaml>=${_mt_pkgver}")

    _inst_dir 'sonar'
}
