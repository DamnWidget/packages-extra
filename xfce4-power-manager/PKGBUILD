# $Id: PKGBUILD 234471 2015-03-22 15:50:59Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>

pkgname=xfce4-power-manager
pkgver=1.4.4
pkgrel=1.1
_git=d980cdb716800fae592eada74ea012e8981e0589
pkgdesc="Power manager for Xfce desktop"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'upower' 'libnotify' 'hicolor-icon-theme')
optdepends=('xfce4-panel: for the Xfce panel plugin'
            'lxpanel: for the LXDE panel plugin')
makedepends=('intltool' 'xfce4-panel' 'lxpanel' 'autogen' 'xfce4-dev-tools')
install=$pkgname.install
source=(#http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        $pkgname-$pkgver-$pkgrel.tar.bz2::http://git.xfce.org/xfce/xfce4-power-manager/snapshot/$pkgname-$_git.tar.bz2
        look-up-the-schema-for-light-lockers-settings-recursively.patch::http://git.xfce.org/xfce/xfce4-power-manager/patch/?id=d058daf755f2ea28e6405975f3c7e7957d9d4df6
        remove-dbus-includes.patch::http://git.xfce.org/xfce/xfce4-power-manager/patch/id=42892ec44912c9eaef1e9e2ad4d96c07b59fabb5
        replace-dbus-binding-tool-with-gdbus-codegen.patch::http://git.xfce.org/xfce/xfce4-power-manager/patch/?id=08b4182abda32902e67de91e1ac7d5bf78c3c600
        port-xfpm-dbus-to-lowlevel-cmds.patch::http://git.xfce.org/xfce/xfce4-power-manager/patch/?id=8e02aafbd0247e93baa267980c8860253907af2a
        port-xfpm-dbus-monitor-to-lowlevel-cmds.patch::http://git.xfce.org/xfce/xfce4-power-manager/patch/?id=0b7bb8c0215ca2d5f51e2c052cd7cfa3f8a0eecc
        removed-dbus-binding-tool-annotation.patch::http://git.xfce.org/xfce/xfce4-power-manager/patch/?id=08a1359cde81558847802669b897ae062d4b33fe
        port-xfpm-errors-to-gdbus-errors.patch::http://git.xfce.org/xfce/xfce4-power-manager/patch/?id=8499aa632ae2445d3cb7de64ced42063db0189e2
        port-to-dbus.patch::http://git.xfce.org/xfce/xfce4-power-manager/patch/?id=721055567c1d0dfccfc519ba492b709a13f3b0fe
        fix-suspend-hibernate-calls.patch::http://git.xfce.org/xfce/xfce4-power-manager/patch/?id=a055fa2e42204b484b6000afe1e519321948c455
        fix-some-warnings.patch::http://git.xfce.org/xfce/xfce4-power-manager/patch/?id=fb190d279fab075e24f40dafc979a5d2565cbd3a)
sha256sums=('9329f0d03eaf1484e3c9f488f3cc21f60188e5b211660113680b7247c582c202'
            '138f3ceeb3c41d917414c59e189f8f967a516e1bc99f4b5588685a669b81c6a0'
            '99d731c5d729b2834700ff14de6ba7018e51f88c9ad9d5e9adb96e4b8274aefb'
            '1afd4b6ee076237b452230eea21e05079de0fae5707384ebd20bb2a638733c7b'
            'ba562b6dfc5c23e0979d3b946ffc629e0889243758fc58f15d1f77d92fb5e2cb'
            'b23c79fb091f752389f1b5c6ff925960bb41b01745c4ab304445665e3a2d1993'
            '49f51a953366e5752f32d46283a933dccf6c5c81c0771d44a407476af1f2f160'
            'f8b7257c9f993a57e12b93bf1c4d9fe7cd4bf7dd6a7712bc0c339ccce4a4997f'
            'd1ce6edfe7632038d52ac6a1b7aa2074f200523bcfbc2fe02258992927e6cc58'
            'c42126fc20f8774c204146b4b824e015307c61af72898ffa8f903d2c2eff93fa'
            'f321fe1a989d1e4eaa4375e25666f906aeb68dfc7a63cdc35bd98302bd5974ce')

prepare() {
  mv "$srcdir/$pkgname-$_git" "$srcdir/$pkgname-$pkgver"
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i $srcdir/look-up-the-schema-for-light-lockers-settings-recursively.patch
  patch -Np1 -i $srcdir/remove-dbus-includes.patch
  patch -Np1 -i $srcdir/replace-dbus-binding-tool-with-gdbus-codegen.patch
  patch -Np1 -i $srcdir/port-xfpm-dbus-to-lowlevel-cmds.patch
  patch -Np1 -i $srcdir/port-xfpm-dbus-monitor-to-lowlevel-cmds.patch
  patch -Np1 -i $srcdir/removed-dbus-binding-tool-annotation.patch
  patch -Np1 -i $srcdir/port-xfpm-errors-to-gdbus-errors.patch
  patch -Np1 -i $srcdir/port-to-dbus.patch
  patch -Np1 -i $srcdir/fix-suspend-hibernate-calls.patch
  patch -Np1 -i $srcdir/fix-some-warnings.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-network-manager \
    --enable-polkit \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
